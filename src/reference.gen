\header{Core Macros}

The core macros are the default macros used for expanding s-expressions into Stanza core forms.

\subheader{Types}

\subsubheader{Function Type}
A function type is specified using the \code{->} operator. The arguments to the function are to the left-hand side of the \code{->}, and the return type of the function is on the right-hand side of the \code{->}.
\code{
(Int, Int) -> Int
}

If there is exactly a single argument, then the parentheses surrounding the argument types may be omitted.
\code{
Int -> Int
}

\subsubheader{Intersection Type}
An intersection type is specified using the \code{&} operator.
\code{
Seqable & Lengthable
}

\subsubheader{Union Type}
A union type is specified using the \code{|} operator.
\code{
Int | String
}

\subsubheader{Captured Type Argument}
A captured type argument is specified by prefixing an identifier with the \code{?} operator.
\code{
?T
}

\subsubheader{Tuple Type}
A tuple type is specified by surrounding a sequence of types between the \code{[]} brackets.
\code{
[Int, String]
}

\subsubheader{Parametric Type}
A parametric type is specified by following an identifier with a sequence of types between the \code{<>} brackets.
\code{
Table<Int, String>
}

\subsubheader{Unknown Type}
The special identifier \code{?} represents the unknown type.
\code{
?
}

\subsubheader{Void Type}
The special identifer \code{Void} represents the void type.
\code{
Void
}

\subheader{Declarations}
\subsubheader{Package Declarations}
The following form declares a new package with no imported packages.
\code{
defpackage mypackage
}

Bindings from other packages may be imported using the \code{import} statement.
\code{
defpackage mypackage :
   import core
   import collections
}

Prefixes may be added to bindings from an imported package. The following form will append the prefix \code{core-} to every imported binding from the \code{core} package, except for bindings named \code{False}, which will instead have the \code{C} prefix.
\code{
defpackage mypackage :
   import core with :
      prefix => core-
      prefix(False) => C
}

In addition to the \code{import} statement, there is also the \code{forward} statement:

\code{
defpackage mypackage :
  import core
  forward mydefs
}

The \code{forward} statement will import the package \code{mydefs} and then expose any \code{public} symbols found in it as if they are coming from the \code{mypackage} package.

\subsubheader{Public Visibility}
Either of the following forms can be used to indicate that the following declaration(s) is/are publicly visible.
\code{
public decl
public : decl
}

The colon version is typically used to indicate the visibility of multiple declarations.
\code{
public :
   val x = 10
   val y = 20
}

\subsubheader{Protected Visibility}
Either of the following forms can be used to indicate that the following declaration(s) has/have protected visibility. This indicates that they can only be referenced from other packages through package-qualified identifiers.
\code{
protected decl
protected : decl
}

\subsubheader{Type Declarations}
The following form defines the type \code{MyType}.
\code{
deftype MyType
}

The following form defines the type \code{MyType} as a subtype of \code{Collection} and \code{Lengthable}.
\code{
deftype MyType <: Collection & Lengthable
}

The following form defines the type \code{MyType}, and also defines the types \code{Int} and \code{String} to be subtypes of \code{MyType}.
\code{
deftype MyType :
   Int <: MyType
   String <: MyType
}

Parametric types may be defined as follows.
\code{
deftype MyType<T>
}

These type parameters may be used in the specification of the types parents.
\code{
deftype MyType<T> <: Collection<T> & Lengthable
}

\subsubheader{Struct Declarations}
The following form defines the struct \code{MyStruct} with the given fields \code{x} of type \code{XType} and \code{y} of type \code{YType}.
\code{
defstruct MyStruct :
   x: XType
   y: YType
}

The above form will expand into:
\list{
\item a type declaration for the type \code{MyStruct}.
\item a constructor function called \code{MyStruct} that takes in two arguments, an \code{XType} and a \code{YType} object and returns a \code{MyStruct} object.
\item two getter functions named \code{x} and \code{y} that return the associated field in the struct.
}

To change the name of the generated constructor function, the following option may be used.
\code{
defstruct MyStruct :
   x: XType
   y: YType
with :
   constructor => #MyStruct
}
The generated constructor function will now be named \code{#MyStruct} instead of \code{MyStruct} with the above form.

To generate a default implementation of a printer, set the \code{printer} option to \code{true}.
\code{
defstruct Person :
  name: String
  age: Int
with:
  printer => true  
}

The generated printer is meant for debugging purposes only and quickly generates a string representation of a simple structure. You should provide your own custom printer if this is not the case.

The parent type of a struct may be provided using the following form.
\code{
defstruct MyStruct <: Parent :
   x: XType
   y: YType
}

To create a parametrically typed struct, type arguments can be given following the type name.
\code{
defstruct MyStruct<T> :
   x: XType
   y: YType
   z: Array<T>
}

Mutable fields may be declared by providing the name of a setter function. In the following example, a setter function called \code{set-x} will be generated for setting the value of the \code{x} field.
\code{
defstruct MyStruct :
   x: XType with: (setter => set-x)
   y: YType
}

The generated \code{set-x} function has the following type signature.
\code{
defn set-x (s:MyStruct, x:XType) -> False
}

To generate field accessors as methods instead of as functions, the following form may be used.
\code{
defstruct MyStruct :
   x: XType with: (as-method => true)
}
The above example will generate the \code{x} getter as a method instead of a function.
\code{
defmethod set-x (s:MyStruct, x:XType) -> False
}

Similar to setters, "updaters" can be defined using the \code{updater} option. In the following example, the \code{x} and \code{y} fields can be "updated" with new values.
\code{
defstruct Point :
  x:Int with: (updater => sub-x)
  y:Int with: (updater => sub-y)
}

An updater takes an initial \code{Point} object and creates a new \code{Point} object by substituting the given value. In the following example the point \code{p1} represents \code{(1,2)}, \code{p2} represents \code{(3,2)}, and \code{p3} represents \code{(1,4)}. Updaters are used frequently when programming with immutable datastructures.
\code{
val p1 = Point(1,2)
val p2 = sub-x(p1, 3)
val p3 = sub-y(p1, 4)
}

To provide default values for field values, the \code{default} option can be used. In the following example, the \code{x} and \code{y} fields will have a default value of 0, if no value is provided.
\code{
defstruct Point :
  x:Int with: (default => 0)
  y:Int with: (default => 0)
}

With the above definition, the user can create a point like this
\code{
val origin = Point()
}

Often for mutable structures, we always want a certain field to be initialized to some value. The following example creates a struct called \code{Person}, whose age is always initialized to zero. 
\code{
defstruct Person :
  name:String
  age:Int with: (init => 0, setter => set-age)
}

A \code{Person} object can be created like this
\code{
val p = Person("Patrick")
set-age(p, 23)
}

The \code{default} and \code{init} options allow for a special value called \code{void} that allows these fields to remain uninitialized. Here is a \code{Person} struct whose age is uninitialized when first created.
\code{
defstruct Person :
  name:String
  age:Int with: (init => void, setter => set-age)
}

Note that it is an error to read from a field that has not yet been initialized.

The \code{ensure} option is used to prevent objects from being created with invalid arguments. It takes a precondition checking function that is meant to call \code{fatal} if the given arguments do not satisfy the intended preconditions. The following example shows a \code{Point} object whose fields are restricted to be strictly positive. 
\code{
defstruct Point :
  x:Int with: (ensure => positive!)
  y:Int with: (ensure => positive!)

defn positive! (field-name:String, value:Int) :
  if value <= 0 :
    fatal("Value %_ given to %_ is not positive." % [value, field-name])
}

The checking function passed to \code{ensure} will be provided two arguments: a \code{String} containing the name of the field, and the argument passed to the constructor.

\subsubheader{Function Declarations}
Argument lists have the following form:
\code{
(x:Int, y:Int, z)
}

Each argument binding consists of a binding expression (typically an identifier) followed by a colon and an optional type for the argument.

Tuples (and nested tuples) may be used for binding expressions in order to deconstruct an argument's value.
\code{
([x, y]:[Int, String], y:Int, z)
}

The following form defines the named function \code{f}.
\code{
defn f (x, y) :
   x + y
}

If no explicit type is given for an argument to a named function, then by default it is assigned the unknown type.

Users may optionally declare the types of the arguments and the return value using the following form.
\code{
defn f (x:Int, y:String) -> Char :
   x + y
}

To declare tail-recursive functions use the \code{defn*} tag in place of the \code{defn} tag in the above forms.

\subsubheader{Keyword Arguments}

Argument bindings can use keyword named arguments by using the following syntax:

\code{
defn f (a:Int -- b:String) :
  println("A: %_  B: %_" % [a, b])
}

Note the \code{--} as a separator between the positional arguments and the keyword arguments. In this example - \code{a} is a positional argument and \code{b} is a "Keyword Argument". The function \code{f} can be invoked in the following ways:

\code{
stanza> f(10, b = "Hello")
A: 10 B: Hello
false
stanza> f(b = "Hello", 10)
A: 10 B: Hello
false
}

Invoking \code{f} without the named keyword \code{b} (ie, treating \code{b} as just another positional argument) is not allowed:

\code{
stanza> f(10, "Hello")
No appropriate function 'f' for argument types (Int, String). Possibilities are:
  f: (Int, b:String) -> False in package 'repl16966'
}

\subsubheader{Optional Arguments}

Both positional arguments and keyword arguments can be made optional by supplying a default value for that argument. Example:

\code{
defn g (a:Int = 10 -- b:String = "Goodbye") :
  println("A: %_ B: %_" % [a, b])
}

In this construction all of the following are valid:

\code{
stanza> g(b = "Hell0")
A: 10 B: Hell0
false
stanza> g(5)
A: 5 B: Goodbye
false
stanza> g()
A: 10 B: Goodbye
stanza> g(15, b = "Back Again")
A: 15 B: Back Again
false
stanza> g(b = "Back Again", 25)
A: 25 B: Back Again
false
}

Using \code{b} as a positional argument will cause a syntax error as before.

\subsubheader{Variadic Arguments}

The following defines a function that takes one integer argument and 0 or more string arguments:

\code{
  defn func (a:Int, b:String ...):
    println("A: %_ len(B): %_ B: %," % [a, length(b), b])
}

In the body of \code{func}, the argument \code{b} will have type \code{Tuple<String>}. If we invoke this function we see the following:

\code{
stanza> func(10)
A: 10 len(B): 0 B: 
false
stanza> func(10, "asdf")
A: 10 len(B): 1 B: asdf
false
stanza> func(10, "asdf", "qwer")
A: 10 len(B): 2 B: asdf, qwer
false
}

We can also pass the argument \code{b} by keyword argument:

\code{
stanza> func(10, b = ["apples", "grapes", "bananas"])
A: 10 len(B): 3 B: apples, grapes, bananas
false
}


\subsubheader{Multi Declarations}

The following form declares a multi, \code{f}, with the given type signature.
\code{
defmulti f (x:Int, y:String) -> String
}

If no explicit types are provided for the arguments or return type, then they have the unknown type by default.
\code{
defmulti f (x, y)
}

\subsubheader{Method Declarations}
The following form declares a method for the multi \code{f}.
\code{
defmethod f (x, y) :
   x + y
}

If no explicit types are provided for the arguments then they are assigned the unknown type by default. Users may optionally declare the types of the arguments and the return value using the following form.
\code{
defmethod f (x:Int, y:String) -> Int :
   x + y
}

To declare tail-recursive methods use the \code{defmethod*} tag in place of the \code{defmethod} tag in the above forms.

\subsubheader{Value Declarations}
The following form declares a value \code{x} of type \code{Int} initialized to the expression \code{v}.
\code{
val x : Int = v
}

If the type is omitted then it is inferred from the given value.
\code{
val x = v
}

Tuples may be used within the binding expression to destructure the given value.
\code{
val [x, [y, z]] = v
}

\subsubheader{Variable Declarations}
The following form declares a variable.
\code{
var x : Int = v
}

If the type is omitted then it is inferred from any assignments to the variable.
\code{
var x = v
}

The initializing expression may be omitted to declare an uninitialized variable. It is an error to read from a variable that is not yet initialized.
\code{
var x
}

\subsubheader{Enum Declarations}
Enumerated types are defined with the \code{defenum} macro:

\code{
defenum Direction :
  N
  E
  S
  W
}

When defining an enum, the macro expands to a \code{deftype} for \code{Direction} and then separate \code{deftype}'s for each of the enumerated values:

\code{
deftype Direction <: Equalable & Hashable
deftype N <: Direction
deftype E <: Direction
deftype S <: Direction
deftype W <: Direction
}

The macro also expands the following functions for ease of use.

\code{
; Direction -> Int and vice versa
defn Direction (i:Int) -> Direction
defn Direction? (i:Int) -> Direction|False
defn to-int (d:Direction) -> Int
; To satisfy Equalable & Hashable
defmethod equal? (a:Direction, b:Direction) -> Int
defmethod hash (x:Direction) -> Int
; Printing
defmethod print (o:OutputStream, x:Direction) -> False
}

Note that the enumerated integer values are ordered and start from 0. In this case N=0, E=1, S=2, W=3.

The \code{defenum} can also take a parent type just like a \code{deftype}:

\code{
public deftype AssetType

public defenum CompilerOutputs <: AssetType :
  ObjectFile
  Executable
  SharedLibrary
}

\subheader{Expressions}
There are only a handful of core forms in Stanza but the core macro library provides a large collection of syntactic sugar for expressing commonly used constructs.

\subsubheader{Literal S-Expressions}
The backtick form represents a literal s-expression. For example, the following form creates a list consisting of three elements: the symbol f, the integer 3, and the symbol g.
\code{
`(f 3 g)
}

\subsubheader{Expression Sequences}
Multiple expressions surrounded with parenthesis represent a sequence of expressions, whose value is equal to the value of the last expression.
\code{
(x + 3, 3, f(3))
}

\subsubheader{Function Calls}
Expressions followed immediately opening parenthesis expand to function call forms.
\code{
f(x)
}

\subsubheader{Polymorphic Function Calls}
Expressions followed by angle brackets then opening parenthesis expand to polymorphic function call forms.
\code{
f<Int>(x)
}

\subsubheader{Calls to Get}
Expressions followed by opening square brackets expand to a call to the \code{get} function.
\code{
f[x, y]
}
expands to
\code{
get(f, x, y)
}

\subsubheader{Calls to Set}
Expressions followed by opening square brackets and then an equal sign expands to a call to the \code{set} function.
\code{
f[x, y] = v
}
expands to
\code{
set(f, x, y, v)
}

\subsubheader{Anonymous Functions}
Expressions wrapped in curly brackets expand to an anonymous function, with underscores replaced by arguments.
\code{\#{
{_ + y}
\#}}
expands to
\code{\#{
fn (x) : x + y
\#}}

For convenience, if there are no underscores, then the created anonymous function can take both no arguments and a single argument.
\code{\#{
{y}
\#}}
expands to
\code{
multifn :
   () : y
   (x) : y
}

\subsubheader{Curried Functions}
Expressions followed by an opening curly bracket expand to an anonymous function, with underscores replaced by arguments.
\code{\#{
f{_, y}
\#}}
expands to
\code{
fn (x) : f(x, y)
}

If there are no underscores, then the created anonymous function can take both no arguments and a single argument.
\code{\#{
f{y}
\#}}
expands to
\code{
multifn :
   () : f(y)
   (x) : f(y)
}

\subsubheader{Casts}
A value can be downcast to a given type using the \code{as} operator.
\code{
x as Int
}

If followed with an \code{else} clause, then the as operator will return the given default value if the expression is not of the given type.
\code{
x as Int else y
}

\subsubheader{Upcasts}
A value can be upcast to a given type using the \code{as?} operator.
\code{
x as? Int
}

\subsubheader{Type Checks}
The following form returns true if the given value is of the type \code{Type} or \code{false} otherwise.
\code{
x is Type
}

The following form returns true if the given value is not of the type \code{Type} or \code{false} otherwise.
\code{
x is-not Type
}

\subsubheader{Tuples}
Tuples are created by surrounding a sequence of expressions with the \code{[]} brackets.
\code{
[x, y]
}

\subsubheader{Objects}
The following form creates a new object of the type \code{MyType} with no instance methods.
\code{
new MyType
}

The following form creates a new object of the type \code{MyType} with instance methods. An instance method follows the same syntax as top-level methods, but requires precisely one argument named \code{this} that refers to the object being created.
\code{
new MyType :
   defmethod f (this, y:Int) :
      x + y
}

\subsubheader{Ranges}
The following form creates a \code{Range} from \code{a} to \code{b} (exclusive) with a step size of 1.
\code{
a to b
}

A step size may be provided explicit using the \code{by} keyword.
\code{
a to b by 2
}

To indicate that the ending index should be inclusive rather than exclusive, the \code{through} keyword may be used in place of the \code{to} keyword.
\code{
a through b
a through b by 2
}

\subsubheader{KeyValue Objects}
The following form creates a \code{KeyValue} object from the given key object \code{k}, and the value object \code{v}.
\code{
k => v
}

\subsubheader{Apply Operator}
The following operator calls the expression \code{f} with the argument \code{x}.
\code{
f $ x
}

It is often used in conjunction with curried functions to chain together long expressions.
\code{\#{
request-price $
{_ * 0.75} $
calc-mortgage{_, 75.00} $
base-price()
\#}}

\subsubheader{Chained Function Operator '$>'}
The following operator allows the functions \code{func1} and \code{func2} to be called in sequence (chained). The input value \code{some-input} is passed as the argument to \code{func1}. \code{func1} is invoked and the output is applied as the argument to \code{func2}.
\code{
some-input $> func1 $> func2
}

Example:

\code{\#{
stanza> val data = ["  what", "a wonderful", "world.   "]
stanza> val msg = data $> string-join{_, " "} $> trim $> upper-case
stanza> println(msg)
WHAT A WONDERFUL WORLD.
\#}}

Here a tuple of strings is joined, trimmed of white-space, and converted to uppercase letters in a chained sequence of operations.

\subsubheader{If Conditionals}
The following form evaluates one of two branches depending on whether the predicate \code{p} is \code{true} or \code{false}.
\code{
if p :
   consequent
else :
   alternate
}

If the \code{else} branch is omitted, then a default \code{else} branch is provided that simply returns \code{false}.
\code{
if p :
   consequent
}

If the \code{else} branch consists of a single \code{if} expression, then the colon following the \code{else} may be omitted. This allows multiple \code{if} expressions to be chained elegantly.
\code{
if p1 :
   consequent1
else if p2 :
   consequent2
else if p3 :
   consequent3
else :
   alternate
}

\subsubheader{When Conditionals}
The following form allows for simple branches on \code{true}/\code{false} to be written concisely.
\code{
consequent when p else alternate
}
expands to:
\code{
if p :
   consequent
else :
   alternate
}

If the \code{else} clause is omitted, then it returns \code{false} by default.

\subsubheader{Match Conditionals}
The following form evaluates one of a multiple of branches depending on the types of its arguments. If no type is given for a branch argument then it is inferred from the type of the input arguments.
\code{
match(x, y) :
   (x:Int, y:Int) : body0
   (x:Int, y:String) : body1
   (x:String, y:Int) : body2
   (x, y) : body3
}

\subsubheader{Switch Conditionals}
The following form creates a chain of \code{if-else} expressions for evaluating a sequence of bodies.
\code{
switch f :
   a : body0
   b : body1
   c : body2
   d : body3
   else : body4
}
expands to:
\code{
if f(a) : body0
else if f(b) : body1
else if f(c) : body2
else if f(d) : body3
else : body4
}

If no \code{else} branch is given, then the default behaviour is to exit the program with a fatal error.

The \code{switch} is commonly used with the \code{equal?} predicate function.
\code{\#{
switch {x == _} :
   1 : first-action()
   2 : second-action()
   3 : third-action()
\#}}

\subsubheader{Let Scopes}
The following form evaluates its body under a new scope.
\code{
let :
   body
}

\subsubheader{Where Scopes}
The following form evaluates an expression with a set of declared bindings.
\code{
exp where :
   body
}
expands to:
\code{
let :
   body
   exp
}

The following example demonstrates assigning to \code{x} the result of \code{42 - (10 * y)}.
\code{
x = (42 - z) where :
   val z = 10 * y
}

\subsubheader{For "Loops"}
The following form calls an operating function, \code{f}, with an anonymous function and a list of arguments.
\code{
for (x in xs, y in ys, z in zs) f :
   body
}
expands to:
\code{
f((fn (x, y, z) : body), xs, ys, zs)
}

If there is only a single set of bindings, then the parenthesis may be omitted.
\code{
for x in xs f :
   body
}

By far, the most common usage of the \code{for} construct is with the \code{do} function to iterate over sequences. The following example prints the integers between \code{0} and \code{10}.
\code{
for i in 0 to 10 do :
   println(i)
}

\subsubheader{Within Blocks}
The following form
\code{
within result = f(x, y, z) :
  body
}
expands into a call to \code{f} with an additional function representing the block body.

The above is equivalent to this:
\code{
f(
  fn (result) : body,
  x,
  y,
  z
)
}
Notice that \code{result} becomes an argument to the block body.

The shorthand form
\code{
within f(x, y, z) :
  body
}
assumes that \code{f} takes a zero-arity function as the first argument.

The above is equivalent to this:
\code{
f(
  fn () : body
  x,
  y,
  z
)
}

As an example, the following creates a function that executes a body with a newly created temporary file. The function takes responsibility for creating a fresh unused file, and then deleting it after the body has finished.
\code{
defn with-temporary-file (body:FileOutputStream -> ?, prefix:String) :
  defn unused-file (i:Int) :
    val name = to-string("%_%_.dat" % [prefix, i])
    if file-exists?(name) : unused-file(i + 1)
    else : name
  val filename = unused-file(0)  
  val file = FileOutputStream(filename)
  try :
    body(file)
  finally :
    close(file)
    delete-file(filename)
}

The user can then use \code{with-temporary-file} like so:
\code{
within file = with-temporary-file("temporary-dump") :
  println(file, "This is temporary data")
  for i in 0 to 10 do :
    println(file, i)
}

\subsubheader{While Loops}
The following form creates a loop that repeats as long as a given predicate returns \code{true}.
\code{
while p :
   body
}

\subsubheader{Labeled Scopes}
The following form creates a labeled scope which returns an object of type \code{Type} and with an exit function named \code{myreturn}.
\code{
label<Type> myreturn :
   body
}

If no explicit type is provided, then the body is assumed to return \code{false}, and the exit function takes no arguments.
\code{
label myreturn :
   body
}

The most common use of labeled scopes is to return early from a function. The following example demonstrates a function that finds the first integer whose square is over \code{100}.
\code{
defn first-big-square () :
   label<Int> return :
      for i in 0 to false do :
         return(i) when i * i > 100 :
      fatal("No such number.")
}


\subsubheader{Generators}
The following form generates a sequence of objects of type \code{Type} given the generator body. Within the body, a distinguished function called \code{yield} takes a single argument and includes it in the resulting sequence. A distinguished function called \code{break} is used to end the sequence. If no argument is passed to \code{break} then the sequence ends immediately. If a single argument is passed to \code{break} then it is included in the resulting sequence before the sequence ends.
\code{
generate<Type> :
   body
}

If no explicit type is provided, then the default type is the unknown type.
\code{
generate :
   body
}

The following example demonstrates a function that returns the first hundred perfect squares.
\code{
generate<Int> :
   for i in 1 through 100 do :
      yield(i * i)
}

To demonstrate the \code{break} function, the above example could also be written with a while loop like so.
\code{
generate<Int> :
   var i = 1
   while true :
      if i == 100 : break(i * i)
      else : yield(i * 1)
      i = i + 1
}

\subsubheader{Dynamic Variables}
The following form temporarily sets the variable \code{x} to the value \code{v} while executing the given body. The variable is restored to its original value after the body is finished.
\code{
let-var x = v :
   body
}

If there is more than one set of bindings, then they are set and restored in parallel.
\code{
let-var (x = v1, y = v2) :
   body
}

\subsubheader{Exception Handling}
The following form executes a given body after installing the given exception handlers and \code{finally} block. Both the exception handlers and the \code{finally} block are optional, but a \code{try} expression must contain either a \code{finally} block or an exception handler. If the \code{finally} block is provided, then it is a fatal error to exit and then re-enter the body through the use of coroutines or generators.
\code{
try :
   body
catch (e:MyException1) :
   handler1
catch (e:MyException2) :
   handler2
finally :
   cleanup
}

\subsubheader{Attempts and Failures}
The following form executes a given body with a given failure handler. If no \code{else} clause is given, then the default behaviour is to return \code{false}.
\code{
attempt :
   body
else :
   handle failure
}

\subsubheader{Operators}
Operators in Stanza expand to simple function calls. The following is a listing of the core operators and their expanded form.
\code{
(- x)     expands to   negate(x)
(~ x)     expands to   bit-not(x)
not x     expands to   complement(x)
x == y    expands to   equal?(x, y)
x != y    expands to   not-equal?(x, y)
x < y     expands to   less?(x, y)
x <= y    expands to   less-eq?(x, y)
x > y     expands to   greater?(x, y)
x >= y    expands to   greater-eq?(x, y)
x + y     expands to   plus(x, y)
x - y     expands to   minus(x, y)
x * y     expands to   times(x, y)
x / y     expands to   divide(x, y)
x % y     expands to   modulo(x, y)
x << y    expands to   shift-left(x, y)
x >> y    expands to   shift-right(x, y)
x >>> y   expands to   arithmetic-shift-right(x, y)
x & y     expands to   bit-and(x, y)
x | y     expands to   bit-or(x, y)
x ^ y     expands to   bit-xor(x, y)
}

\subsubheader{Anonymous Functions}
The following form creates an anonymous function with two arguments.
\code{
fn (x, y) :
   x + y
}

If no explicit type is given for an argument then it will be inferred from the context in which the function is used. Users may optionally declare the types of the arguments and the return value using the following form.
\code{
fn (x:Int, y:String) -> Char :
   x + y
}

To make an anonymous function tail recursive, use the \code{fn*} tag in place of the \code{fn} tag in the above forms.

\subsubheader{Multiple Arity Functions}
The following forms creates an anonymous function with multiple arities.
\code{
multifn :
   (x) : x + 1
   (x, y) : x + 2
}

If no explicit type is given for an argument then it will be inferred from the context in which the function is used. Users may optionally declare the types of the arguments and the return values using the following form.
\code{
multifn :
   (x:Int) -> Int : x + 1
   (x:Int, y:String) -> String : x + 2
}

To make a multiple arity function tail recursive, use the \code{multifn*} tag in place of the \code{multifn} tag in the above forms.

\subsubheader{Variable Assignments}
The following form assigns the expression \code{v} to the variable \code{x}. An assignment expression returns \code{false}.
\code{
x = v
}

\subsubheader{Quasi Quote}
The \code{qquote} form returns an s-expression with indicated expressions substituted in.
\code{
qquote(a b c)
}

The above example returns the list \code{(a b c)}. The following example demonstrates the \code{~} substitution operator and returns the list \code{(a 3 c)}.
\code{
val b = 3
qquote(a ~ b c)
}

The following example demonstrates the \code{~@} splicing operator and returns the list returns the list \code{(a 1 2 3 c)}.
\code{
val b = `(1 2 3)
qquote(a ~@ b c)
}

\subsubheader{Compile Time Flags}
The following form will define the given compile-time flag.
\code{
#define(flag)
}

The following form expands into the consequent if the given compile-time flag is defined or into the alternate otherwise.
\code{
#if-defined(flag) :
   consequent
#else :
   alternate
}

If the \code{#else} branch is not provided then it expands into the empty expression \code{()} if the flag is not defined.

The following form expands into the consequent if the given compile-time flag is not defined or into the alternate otherwise.
\code{
#if-not-defined(flag) :
   consequent
#else :
   alternate
}

If the \code{#else} branch is not provided then it expands into the empty expression \code{()} if the flag is defined.


\header{Core Package}
The \code{core} package consists of functions and types used for basic programming.

\subheader{Equalable}
Values that support the \code{equal?} operation are indicated as subtypes of Equalable.
\code{
deftype Equalable
}

Mandatory minimal implementation: \code{equal?}.

\subsubheader{equal?}
The multi \code{equal?} takes as arguments two \code{Equalable} values and returns \code{true} if they are equal or \code{false} otherwise.
\code{
defmulti equal? (a:Equalable, b:Equalable) -> True|False
}

A default method for equal? is provided that simply returns \code{false}.

Equality in Stanza is defined to mean "invariant to substitution". That is, suppose that our program contains a call to a function, \code{f}, with a value \code{x}.
\code{
f(x)
}

But suppose, that earlier, we have determined that
\code{
x == y
}
returns \code{true}, indicating that \code{x} is equal to \code{y}. Then we should we able to substitute the usage of \code{x} with \code{y} instead without changing the behaviour of the program.
\code{
f(y)
}

Consistent to this definition, in Stanza's core library, values of immutable types are defined to be equal if their subfields are equal. Different mutable values, such as arrays, are never defined to be equal unless they refer to the same object.

\subsubheader{not-equal?}
\code{not-equal?} returns \code{true} if its two arguments are not equal and \code{false} otherwise.
\code{
defn not-equal? (a:Equalable, b:Equalable) -> True|False
}

\subheader{Comparable}
The \code{Comparable} type is used to indicate that a value can compared against other values using the comparison operations. A value of type \code{Comparable<T>} can be compared against values of type \code{T}.
\code{
deftype Comparable<T>
}

Mandatory minimal implementation: \code{compare}.

\subsubheader{compare}
The following multi compares values of type \code{Comparable<T>} against values of type \code{T}. The multi returns a negative integer if the item \code{a} is less than the item \code{b}, zero if the two are equal, and a positive integer if the item \code{a} is greater than the item \code{b}. For values that are subtypes of both \code{Equalable} and \code{Comparable}, the compare operation must be consistently defined against the \code{equal?} operation, and return \code{0} if \code{equal?} returns \code{true}.
\code{
defmulti compare<?T> (a:Comparable<?T>, b:T) -> Int
}

\subsubheader{less?, greater?, less-eq?, greater-eq?}
The following multis are provided for subtypes to provide efficient implementations. Any defined method must return results consistent with the compare operation. Default methods are provided for these multis defined in terms of the \code{compare} operation.
\code{
defmulti less?<?T> (a:Comparable<?T>, b:T) -> True|False
defmulti greater?<?T> (a:Comparable<?T>, b:T) -> True|False
defmulti less-eq?<?T> (a:Comparable<?T>, b:T) -> True|False
defmulti greater-eq?<?T> (a:Comparable<?T>, b:T) -> True|False
}

\subsubheader{max, min}

The following functions compute the maximum or minimum of their two given arguments.
\code{
defn max<?T,?S> (a:?S&Comparable<?T>, b:T) -> S|T
defn min<?T,?S> (a:?S&Comparable<?T>, b:T) -> S|T
}

\subsubheader{maximum, minimum}

The following functions compute the minimum between an initial value \code{x0}, and a sequence of items \code{xs} using the provided comparison function. If no initial value is provided, then the sequence \code{xs} cannot be empty.
\code{
defn minimum<?T> (x0:T, xs:Seqable<?T>, less?: (T, T) -> True|False) -> T
defn minimum<?T> (xs:Seqable<?T>, less?: (T, T) -> True|False) -> T
}

The following functions compute the maximum between an initial value \code{x0}, and a sequence of items \code{xs} using the provided comparison function. If no initial value is provided, then the sequence \code{xs} cannot be empty.
\code{
defn maximum<?T> (x0:T, xs:Seqable<?T>, less?: (T, T) -> True|False) -> T
defn maximum<?T> (xs:Seqable<?T>, less?: (T, T) -> True|False) -> T
}

The following functions compute the minimum between an initial value \code{x0}, and a sequence of comparable items \code{xs} using the \code{less?} multi. If no initial value is provided, then the sequence \code{xs} cannot be empty.
\code{
defn minimum<?T> (x0:T&Comparable, xs:Seqable<?T&Comparable>) -> T
defn minimum<?T> (xs:Seqable<?T&Comparable>) -> T
}

The following functions compute the maximum between an initial value \code{x0}, and a sequence of comparable items \code{xs} using the \code{less?} multi. If no initial value is provided, then the sequence \code{xs} cannot be empty.
\code{
defn maximum<?T> (x0:T&Comparable, xs:Seqable<?T&Comparable>) -> T
defn maximum<?T> (xs:Seqable<?T&Comparable>) -> T
}

The following function returns the minimum in a sequence of items \code{xs} by comparing the keys extracted from each item using the provided \code{key} function.
\code{
defn minimum<?T> (key: T -> Comparable, xs:Seqable<?T>) -> T
}

The following function returns the maximum in a sequence of items \code{xs} by comparing the keys extracted from each item using the provided \code{key} function.
\code{
defn maximum<?T> (key: T -> Comparable, xs:Seqable<?T>) -> T
}

\subheader{Hashable}

The type \code{Hashable} indicates that a value supports the \code{hash} multi and can represented (non-uniquely) as an \code{Int} value.
\code{
deftype Hashable
}

Mandatory minimal implementation: \code{hash}.

\subsubheader{hash}
The \code{hash} multi takes a \code{Hashable} argument and returns an \code{Int}. For the correct operation of \code{HashTable} implementations, the definition of \code{hash} must be consistent with the definition of \code{equal?}. If two values are equal, then they must also return the same hash.
\code{
defmulti hash (h:Hashable) -> Int
}

\subheader{Lengthable}
A \code{Lengthable} is a value that has an unknown length.
\code{
deftype Lengthable
}

Mandatory minimal implementation: \code{length}.

\subsubheader{length}
The \code{length} multi calculates the length of a \code{Lengthable} object.
\code{
defmulti length (l:Lengthable) -> Int
}

\subheader{Seq}
A \code{Seq} represents a possibly infinite sequence of items of type \code{T}.
\code{
deftype Seq<T> <: Seqable<T>
}

Mandatory minimal implementation: \code{empty?}, \code{next}, \code{peek}.

\subsubheader{empty?}
The \code{empty?} multi returns \code{true} if the sequence contains more items, or \code{false} otherwise.
\code{
defmulti empty? (s:Seq) -> True|False
}

\subsubheader{next}
The \code{next} multi returns the next item in the sequence. Repeated calls to \code{next} results in successive items in the sequence being returned. It is a fatal error to call \code{next} on an empty sequence.
\code{
defmulti next<?T> (s:Seq<?T>) -> T
}

\subsubheader{peek}
The \code{peek} multi inspects the next item in the sequence but does not consume it. Repeated calls to \code{peek} results in the same item being returned. It is a fatal error to call \code{peek} on an empty sequence.
\code{
defmulti peek<?T> (s:Seq<?T>) -> T
}

\subheader{Seqable}
A \code{Seqable} represents any object that may be viewed as a sequence of items of type \code{T}.
\code{
deftype Seqable<T>
}

Mandatory minimal implementation: \code{to-seq}.

\subsubheader{to-seq}
The \code{to-seq} multi returns a \code{Seq} representing a sequential view of the contents of the given object \code{s}.
\code{
defmulti to-seq<?T> (s:Seqable<?T>) -> Seq<T>
}

A value of type \code{Seq} is defined to also be a \code{Seqable} and trivially returns itself when called with \code{to-seq}.

\subheader{Collection}
Similar to \code{Seqable}, a \code{Collection} also represents an object that may be viewed as a sequence of items of type \code{T}. The crucial difference between a \code{Collection} and \code{Seqable} is that a \code{Collection} must be \italic{repeatedly} viewable as a sequence of items.

As an example, consider a function, \code{print-all-twice}, that iterates through and prints out the items in a sequence twice.
\code{
defn print-all-twice (xs:Seqable) -> False :
   val seq1 = to-seq(xs)
   while not empty?(seq1) :
      println(next(seq1))

   val seq2 = to-seq(xs)
   while not empty?(seq2) :
      println(next(seq2))
}

Such a function would not work as expected when called with a \code{Seq}. Recall that calling \code{to-seq} on a \code{Seq} is a trivial operation that returns the \code{Seq} directly. Thus the first while loop will print out every item in the sequence, but the second while loop will not complete a single iteration as the sequence is already empty. The correct type signature for \code{print-all-twice} would be
\code{
defn print-all-twice (xs:Collection) -> False
}

\code{Seq} is a subtype of \code{Seqable}, but not a subtype of \code{Collection}. Thus the new type signature correctly disallows \code{print-all-twice} from being called with objects of type \code{Seq}.

Mandatory minimal implementation: \code{to-seq}.

\subheader{IndexedCollection}

An \code{IndexedCollection} represents a mutable collection containing a series of items, each of which is associated with an integer index.

\code{deftype IndexedCollection<T> <: Lengthable & Collection<T>}

All \code{IndexedCollections} are subtypes of \code{Collection} and support the \code{to-seq} operation. A default method for \code{to-seq} defined in terms of \code{length} and \code{get} is provided. For efficiency purposes, subtypes of \code{IndexedCollections} may provide a customized method of \code{to-seq}, but it is not required.

Mandatory minimal implementation: \code{length}, \code{get(IndexedCollection<T>, Int)}, \code{set(IndexedCollection<T>, Int, T)}.

\subsubheader{get, set}
Appropriate methods for getting and setting an item at a given index \code{i} must be provided for all subtypes. \code{get} retrieves the object at integer index \code{i} in the object \code{a}. \code{set} assigns the value \code{v} to the object \code{a} at integer index \code{i}. The index \code{i} must be non-negative and less than the length of \code{a}.
\code{
defmulti get<?T> (a:IndexedCollection<?T>, i:Int) -> T
defmulti set<?T> (a:IndexedCollection<?T>, i:Int, v:T) -> False
}

\subsubheader{get}
The following function returns a range of items in an \code{IndexedCollection}. The given range must be a \italic{dense index range} with respect to the collection \code{a}. A default method defined in terms of \code{get} is provided, but subtypes may provide customized methods for efficiency purposes if desired.
\code{
defmulti get<?T> (a:IndexedCollection<?T>, r:Range) -> Collection<T>
}

\subsubheader{set}
The following function sets a range of indices within the collection to items taken sequentially from \code{vs}. The given range must be a \italic{dense index range} with respect to the collection \code{a}. The sequence \code{vs} must contain at least as many items as indices being assigned. A default method defined in terms of \code{set} is provided, but subtypes may provide customized methods for efficiency purposes if desired.
\code{
defmulti set<?T> (a:IndexedCollection<?T>, r:Range, vs:Seqable<T>) -> False
}

\subsubheader{map!}
The following function iterates through the given collection and replaces each item with the result of calling \code{f} on the item. A default method defined in terms of \code{get} and \code{set} is provided, but subtypes may provide customized methods for efficiency purposes if desired.
\code{
defmulti map!<?T> (f: T -> T, xs:IndexedCollection<?T>) -> False
}

\subsubheader{empty?}
\code{empty?} returns \code{true} if the collection is empty or \code{false} otherwise.
\code{
defn empty? (v:IndexedCollection) -> True|False
}

\subsubheader{reverse!}
The following function reverses the order in which items appear in the collection.
\code{
defn reverse!<?T> (xs:IndexedCollection<?T>) -> False
}

\subsubheader{in-reverse}
The following function returns a sequence containing the items in the collection in reversed order. The original collection is unchanged.
\code{
defn in-reverse<?T> (xs:IndexedCollection<?T>) -> Seq<T>
}


\subheader{OutputStream}
An \code{OutputStream} represents a destination to which we can print characters, and write values. The most common output stream used in daily programming is the standard output stream which represents the user's terminal.
\code{
deftype OutputStream
}

Mandatory minimal implementation: \code{print(OutputStream, Char)}.

\subsubheader{print}
The fundamental operation for an \code{OutputStream} is the function for printing a specific value \code{x} to the output stream.
\code{
defmulti print (o:OutputStream, x) -> False
}

For all types in the core Stanza library, there is a default implementation of \code{print} for that type that prints each \code{Char} in its string representation to the output stream. Because of this, the only mandatory method that needs to be implemented by subtypes of \code{OutputStream} is that for \code{Char}.
\code{
defmethod print (o:OutputStream, c:Char) -> False
}

\subsubheader{print-all}
\code{print-all} prints all the item in the given sequence to the \code{OutputStream}.
\code{
defmulti print-all (o:OutputStream, xs:Seqable) -> False
}

There is a default method implemented for \code{print-all} that calls \code{print} on each item in the sequence. For efficiency purposes, users are free to provide customized versions of \code{print-all} for specific types.

\subsubheader{println}
\code{println} prints the item \code{x} to the \code{OutputStream} followed by the newline character.
\code{
defn println (o:OutputStream, x) -> False
}

\subsubheader{println-all}
\code{println-all} prints all items in the sequence \code{xs} to the \code{OutputStream} followed by the newline character.
\code{
defn println-all (o:OutputStream, xs:Seqable) -> False
}

\subsubheader{STANDARD-OUTPUT-STREAM}
This global value holds the standard output stream representing the user's terminal.
\code{
val STANDARD-OUTPUT-STREAM : OutputStream
}

\subsubheader{STANDARD-ERROR-STREAM}
This global value holds the standard error stream. It represents the user's terminal if error messages are not redirected or the error buffer otherwise.
\code{
val STANDARD-ERROR-STREAM : OutputStream
}

\subsubheader{with-output-stream}
The current output stream is, by default, the standard output stream. The following function will set the current output stream to \code{o} before calling the function \code{f} and then restore the current output stream afterwards.
\code{
defn with-output-stream<?T> (o:OutputStream, f: () -> ?T) -> T
}

\subsubheader{current-output-stream}
The current output stream may be retrieved with the following function.
\code{
defn current-output-stream () -> OutputStream
}

\subsubheader{print to current OutputStream}
The following functions behave identically to the versions that do not take an OutputStream argument. Instead they print to the current output stream.
\code{
defn print (x) -> False
defn println (x) -> False
defn print-all (xs:Seqable) -> False
defn println-all (xs:Seqable) -> False
}

\subheader{InputStream}
An \code{InputStream} represents a source from which we can read characters and values.
\code{
deftype InputStream
}

Mandatory minimal implementation: \code{get-char}, \code{get-byte}.

\subsubheader{get-char}
This multi reads a single character from the given input stream. \code{false} is returned if there are no more characters in the stream.
\code{
defn get-char (i:InputStream) -> Char|False
}

\subsubheader{get-byte}
The following function reads a single byte from the given input stream. \code{false} is returned if there are no more bytes in the stream.
\code{
defn get-byte (i:InputStream) -> Byte|False
}

\subsubheader{STANDARD-INPUT-STREAM}
This global value holds the standard input stream representing the user's terminal.
\code{
val STANDARD-INPUT-STREAM : InputStream
}

\subsubheader{fill}
The following multi reads characters continuously from the input stream and stores them into the given \code{CharArray} at the specified range. The range must be a \italic{dense index range} with respect to \code{xs}. The number of characters read is returned.
\code{
defmulti fill (xs:CharArray, r:Range, i:InputStream) -> Int
}

A default method implemented in terms of \code{get-char} is provided, but for efficiency purposes, users are free to provide customized methods for subtypes of \code{InputStream}.

\subheader{IndentedStream}
An \code{IndentedStream} wraps over an \code{OutputStream} to provide the ability to automatically print indenting spaces as needed.
\code{
deftype IndentedStream <: OutputStream
}

\code{IndentedStream} is a subtype of \code{OutputStream} and can be used as a target for the \code{print} function. When asked to print a newline character, an \code{IndentedStream} will print the newline character followed by the number of spaces indicated during creation of the stream. For all other characters, an \code{IndentedStream} simply calls print on its wrapped \code{OutputStream}.

\subsubheader{IndentedStream}
This function creates an \code{IndentedStream} that indents \code{n} spaces by wrapping over the given \code{OutputStream}.
\code{
defn IndentedStream (o:OutputStream, n:Int) -> IndentedStream
}

\subsubheader{do-indented}
This function calls the function \code{f} with a new \code{IndentedStream} created by wrapping over the given output stream.
\code{
defn do-indented<?T> (f: IndentedStream -> ?T, o:OutputStream) -> T
}

\subsubheader{indented}
This function wraps over the current output stream with an \code{IndentedStream} and then calls the function \code{f}. The current output stream is restored afterwards.
\code{
defn indented<?T> (f: () -> ?T) -> T
}

\subheader{FileOutputStream}
A \code{FileOutputStream} represents an external file with an output stream interface to which we can write values.
\code{
deftype FileOutputStream <: OutputStream
}

A \code{FileOutputStream} is a subtype of \code{OutputStream} and can be used as a target for the \code{print} function.

\subsubheader{FileOutputStream}
These functions create a new \code{FileOutputStream} given the path to the file, and a boolean flag indicating, in the case that the file already exists, whether new characters should be appended to the end of the file or whether the file should be overwritten. If no \code{append?} flag is given, then by default, the file is overwritten.
\code{
defn FileOutputStream (filename:String, append?:True|False) -> FileOutputStream
defn FileOutputStream (filename:String) -> FileOutputStream
}

\subsubheader{put}
These functions write the following values as binary data following little endian conventions.
\code{
defn put (o:FileOutputStream, x:Byte) -> False
defn put (o:FileOutputStream, x:Int) -> False
defn put (o:FileOutputStream, x:Long) -> False
defn put (o:FileOutputStream, x:Float) -> False
defn put (o:FileOutputStream, x:Double) -> False
}

\subsubheader{close}
This function closes a \code{FileOutputStream}.
\code{
defn close (o:FileOutputStream) -> False
}

\subsubheader{with-output-file}
This function sets the given file as the current output stream before calling the function \code{f}. Afterwards, it restores the current output stream and closes the file.
\code{
defn with-output-file<?T> (file:FileOutputStream, f: () -> ?T) -> T
}

\subsubheader{spit}
This function prints the given object to the given file.
\code{
defn spit (filename:String, x) -> False
}

\subheader{Pretty Printing}
The following pretty printing functions are used for printing a value in a form that preserves its structure. For example, \code{String} objects are surrounded in quotes and non-printable characters are escaped. Numbers have suffixes to indicate their type. For the core types that make up an s-expression, values are written out in a form that can be read back in using the reader.

\subsubheader{write}
The write multi pretty prints the given object \code{x}. A default method is provided that simply calls \code{print}. Objects whose pretty printed representations differ from their printed representations should provide a custom method for \code{write}.
\code{
defmulti write (o:OutputStream, x) -> False
}

\subsubheader{write-all}
This function pretty prints every item in the sequence \code{xs} separated by spaces.
\code{
defn write-all (o:OutputStream, xs:Seqable) -> False
}

\subheader{FileInputStream}
A \code{FileInputStream} represents an external file with an input stream interface from which we can read values.
\code{
deftype FileInputStream <: InputStream
}

\code{FileInputStream} is a subtype of \code{InputStream} and supports the fundamental operations, \code{get-char} and \code{get-byte}, for reading a character or a byte from the stream.

\subsubheader{FileInputStream}
This function creates a \code{FileInputStream} given the name of the file.
\code{
defn FileInputStream (filename:String) -> FileInputStream
}

\subsubheader{close}
This function closes a \code{FileInputStream}.
\code{
defn close (i:FileInputStream) -> False
}

\subsubheader{Reading Values}
These functions read values as binary data following little endian conventions.
\code{
defn get-int (i:FileInputStream) -> False|Int
defn get-long (i:FileInputStream) -> False|Long
defn get-float (i:FileInputStream) -> False|Float
defn get-double (i:FileInputStream) -> False|Double
}

\subsubheader{slurp}
This function reads the entire contents of a file and returns the contents as a \code{String}.
\code{
defn slurp (filename:String) -> String
}

\subheader{StringInputStream}
A \code{StringInputStream} represents an input stream backed by an underlying string.
\code{
deftype StringInputStream <: InputStream & Lengthable
}

\code{StringInputStream} is a subtype of \code{InputStream} and implements appropriate methods for reading characters and bytes.

\code{StringInputStream} is a subtype of \code{Lengthable} and implements an appropriate method for the \code{length} multi that returns the number of characters left unread in the underlying string.

\subsubheader{StringInputStream}
This function creates a \code{StringInputStream} given the underlying string and the name of the file that it comes from. If no filename is given, then its default value is \code{"UnnamedStream"}.
\code{
defn StringInputStream (string:String, filename:String) -> StringInputStream
defn StringInputStream (string:String) -> StringInputStream
}

\subsubheader{peek?}
A \code{StringInputStream} allows for the characters beyond the immediate following character to be read. The following function returns the \code{i}'th next character in the stream, if one exists. If the index \code{i} is not given, then its default value is \code{0}.
\code{
defn peek? (s:StringInputStream, i:Int) -> False|Char
defn peek? (s:StringInputStream) -> False|Char
}

\subsubheader{info}
\code{StringInputStream} keeps track of the current position of its read marker. The following function returns the \code{FileInfo} representing the current position in the string.
\code{
defn info (s:StringInputStream) -> FileInfo
}

\subheader{RandomAccessFile}
A \code{RandomAccessFile} represents an external file that allows users to read and write at non-consecutive locations.
\code{
deftype RandomAccessFile
}

\subsubheader{RandomAccessFile}
This function creates a \code{RandomAccessFile} for the given filename and boolean flag for indicating whether the file should be writable. A \code{FileOpenException} is thrown if the file could not be opened.
\code{
defn RandomAccessFile (filename:String, writable:True|False) -> RandomAccessFile
}

\subsubheader{close}
This function flushes all pending writes and closes an open \code{RandomAccessFile}. A \code{FileCloseException} is thrown if the file could not be closed.
\code{
defn close (file:RandomAccessFile) -> False
}

\subsubheader{writable?}
This function returns \code{true} if the given file is writable, or false otherwise.
\code{
defn writable? (file:RandomAccessFile) -> True|False
}

\subsubheader{length}
This function returns the length in bytes for the given file.
\code{
defn length (file:RandomAccessFile) -> Long
}

\subsubheader{set-length}
This function sets the length in bytes of the given file to the given value. If the current length of the file is greater than the given length, then the file is truncated. If the current length is less than the given length, then the file is padded with undefined values until the given length. A \code{FileSetLengthException} is thrown if the operation is not successful.
\code{
defn set-length (file:RandomAccessFile, length:Long) -> False
}

\subsubheader{seek}
This function sets the read/write cursor of the file to the given position. A \code{FileSeekException} is thrown if the operation is not successful.
\code{
defn seek (file:RandomAccessFile, pos:Long) -> False
}

\subsubheader{skip}
This function increments the read/write cursor of the file by \code{pos} number of bytes. The cursor can be moved backwards towards the beginning of the file by providing a negative \code{pos}. A \code{FileSeekException} is thrown if the operation is not successful.
\code{
defn skip (file:RandomAccessFile, pos:Long) -> False
}

\subsubheader{Reading Values}
These functions read values as binary data following little endian conventions from the given file. The functions either return the value read, or \code{false} if it has reached the end of the file. A \code{FileReadException} is thrown if the operations are not successful.
\code{
defn get-byte (file:RandomAccessFile) -> Byte|False
defn get-char (file:RandomAccessFile) -> Char|False
defn get-int (file:RandomAccessFile) -> Int|False
defn get-long (file:RandomAccessFile) -> Long|False
defn get-float (file:RandomAccessFile) -> Float|False
defn get-double (file:RandomAccessFile) -> Double|False
}

\subsubheader{Writing Values}
These functions write values as binary data following little endian conventions into the given file. A \code{FileWriteException} is thrown if the operations are not successful.
\code{
defn put (file:RandomAccessFile, x:Byte) -> False
defn put (file:RandomAccessFile, x:Char) -> False
defn put (file:RandomAccessFile, x:Int) -> False
defn put (file:RandomAccessFile, x:Long) -> False
defn put (file:RandomAccessFile, x:Float) -> False
defn put (file:RandomAccessFile, x:Double) -> False
}

\subsubheader{Reading Blocks}
This function reads contiguous bytes from the given file and stores them into the range \code{r} in the byte array \code{a}. \code{r} must be a dense index range with respect to \code{a}. The number of bytes read is returned by the function. A \code{FileReadException} is thrown if the operation is not successful.
\code{
defn fill (a:ByteArray, r:Range, file:RandomAccessFile) -> Long
}

If the range \code{r} is not given, then the function stores the read bytes starting from the beginning of the array \code{a} and proceeds until the end. 
\code{
defn fill (a:ByteArray, file:RandomAccessFile) -> Long
}

\subsubheader{Writing Blocks}
This function writes contiguous bytes from the range \code{r} in the given byte array \code{xs} into the given file. \code{r} must be a dense index range with respect to \code{xs}. A \code{FileWriteException} is thrown if the operation is not successful.
\code{
defn put (file:RandomAccessFile, xs:ByteArray, r:Range) -> False
}

If the range \code{r} is not given, then the function writes the entire array \code{xs} into the file.
\code{
defn put (file:RandomAccessFile, xs:ByteArray) -> False
}

\subheader{Numbers}
The following types make up Stanza's basic numerical types.
\code{
deftype Byte <: Equalable & Hashable & Comparable<Byte>
deftype Int <: Equalable & Hashable & Comparable<Int>
deftype Long <: Equalable & Hashable & Comparable<Long>
deftype Float <: Equalable & Hashable & Comparable<Float>
deftype Double <: Equalable & Hashable & Comparable<Double>
}

A \code{Byte} represents an 8-bit unsigned integer between \code{0} and \code{255} (inclusive). An \code{Int} represents a 32-bit signed integer. A \code{Long} represents a 64-bit signed integer. A \code{Float} represents a 32-bit real number in IEEE 754 encoding. A \code{Double} represents a 64-bit real number in IEEE 754 encoding.

Each of the numerical types are subtypes of \code{Equalable}, and hence support the \code{equal?} multi. Note that numerical values of different types are never defined to be equal to one another. Thus the \code{Int}, \code{0}, is not equal to the \code{Long}, \code{0L}.

Each of the numerical types are subtypes of \code{Comparable}, and can be compared against themselves.

Each of the numerical types are subtypes of \code{Hashable}, and supports the \code{hash} multi.

\subsubheader{Integer Arithmetic}
Integer numerical types support the standard arithmetic operations.
\code{
defn plus (x:Byte, y:Byte) -> Byte
defn minus (x:Byte, y:Byte) -> Byte
defn times (x:Byte, y:Byte) -> Byte
defn divide (x:Byte, y:Byte) -> Byte
defn modulo (x:Byte, y:Byte) -> Byte
}
\code{
defn plus (x:Int, y:Int) -> Int
defn minus (x:Int, y:Int) -> Int
defn times (x:Int, y:Int) -> Int
defn divide (x:Int, y:Int) -> Int
defn modulo (x:Int, y:Int) -> Int
}
\code{
defn plus (x:Long, y:Long) -> Long
defn minus (x:Long, y:Long) -> Long
defn times (x:Long, y:Long) -> Long
defn divide (x:Long, y:Long) -> Long
defn modulo (x:Long, y:Long) -> Long
}

\subsubheader{Floating Point Arithmetic}
Floating point numerical types support the all standard arithmetic operations except the modulo operation.

\code{
defn plus (x:Float, y:Float) -> Float
defn minus (x:Float, y:Float) -> Float
defn times (x:Float, y:Float) -> Float
defn divide (x:Float, y:Float) -> Float
}

\code{
defn plus (x:Double, y:Double) -> Double
defn minus (x:Double, y:Double) -> Double
defn times (x:Double, y:Double) -> Double
defn divide (x:Double, y:Double) -> Double
}

Note that arithmetic operations must be performed between values of the same numerical type. Users must manually convert values from one type to another if their types differ.

\subsubheader{Signed Arithmetic}
All numerical values except of type \code{Byte} support the additional negation and absolute value operations. \code{Byte} values are unsigned and hence do not support these operations.
\code{
defn negate (x:Int) -> Int
defn negate (x:Long) -> Long
defn negate (x:Double) -> Double
defn negate (x:Float) -> Float
}
\code{
defn abs (x:Int) -> Int
defn abs (x:Long) -> Long
defn abs (x:Double) -> Double
defn abs (x:Float) -> Float
}

\subsubheader{Bitwise Operations}
Integer numerical types support the standard bitwise operations.
\code{
defn shift-left (x:Byte, y:Byte) -> Byte
defn shift-right (x:Byte, y:Byte) -> Byte
defn bit-or (x:Byte, y:Byte) -> Byte
defn bit-and (x:Byte, y:Byte) -> Byte
defn bit-xor (x:Byte, y:Byte) -> Byte
defn bit-not (x:Byte) -> Byte
}
\code{
defn shift-left (x:Int, y:Int) -> Int
defn shift-right (x:Int, y:Int) -> Int
defn arithmetic-shift-right (x:Int, y:Int) -> Int
defn bit-or (x:Int, y:Int) -> Int
defn bit-and (x:Int, y:Int) -> Int
defn bit-xor (x:Int, y:Int) -> Int
defn bit-not (x:Int) -> Int
}
\code{
defn shift-left (x:Long, y:Long) -> Long
defn shift-right (x:Long, y:Long) -> Long
defn arithmetic-shift-right (x:Long, y:Long) -> Long
defn bit-or (x:Long, y:Long) -> Long
defn bit-and (x:Long, y:Long) -> Long
defn bit-xor (x:Long, y:Long) -> Long
defn bit-not (x:Long) -> Long
}

\subsubheader{Numerical Limits}
The maximum and minimum values for the integer numerical types are defined in the following global values.
\code{
val BYTE-MAX : Byte
val BYTE-MIN : Byte
val INT-MAX : Int
val INT-MIN : Int
val LONG-MAX : Long
val LONG-MIN : Long
}

\subsubheader{Numerical Conversion}
Numerical types can be converted from one type to another using the following functions.
\code{
defn to-byte (b:Byte) -> Byte
defn to-byte (i:Int) -> Byte
defn to-byte (l:Long) -> Byte
defn to-byte (f:Float) -> Byte
defn to-byte (d:Double) -> Byte
defn to-int (b:Byte) -> Int
defn to-int (i:Int) -> Int
defn to-int (l:Long) -> Int
defn to-int (f:Float) -> Int
defn to-int (d:Double) -> Int
defn to-long (b:Byte) -> Long
defn to-long (i:Int) -> Long
defn to-long (l:Long) -> Long
defn to-long (f:Float) -> Long
defn to-long (d:Double) -> Long
defn to-float (b:Byte) -> Float
defn to-float (i:Int) -> Float
defn to-float (l:Long) -> Float
defn to-float (f:Float) -> Float
defn to-float (d:Double) -> Float
defn to-double (b:Byte) -> Double
defn to-double (i:Int) -> Double
defn to-double (l:Long) -> Double
defn to-double (f:Float) -> Double
defn to-double (d:Double) -> Double
}

Integer types can be converted without loss of precision from a type with less bits to a type with more bits. When converting from a type with more bits to a type with less bits, the most significant bits are truncated.

When converting integer types to floating point types, the closest floating point number is returned.

\subsubheader{bits}
\code{bits} returns an integer type whose bit representation is equivalent to the IEEE754 bit representation of the given floating point number.
\code{
defn bits (x:Float) -> Int
defn bits (x:Double) -> Long
}

\subsubheader{bits-as-float, bits-as-double}
\code{bits-as-float} and \code{bits-as-double} returns a floating point number whose IEEE754 bit representation is equivalent to the bit representation of the given integer.
\code{
defn bits-as-float (x:Int) -> Float
defn bits-as-double (x:Long) -> Double
}

\subsubheader{rand}
The following function generates a random positive integer.
\code{
defn rand () -> Int
}

The following function generates a random positive integer that is guaranteed to be strictly less than n.
\code{
defn rand (n:Int) -> Int
}

The following function generates a random positive integer within the range specified by \code{r}. \code{r} must have a step size of one.
\code{
defn rand (r:Range) -> Int
}

\subsubheader{ceil-log2}
\code{ceil-log2} computes the ceiling of the base-2 logarithm of the integer \code{i}.
\code{
defn ceil-log2 (i:Int) -> Int
defn ceil-log2 (i:Long) -> Int
}

\subsubheader{floor-log2}
\code{floor-log2} computes the floor of the base-2 logarithm of the integer \code{i}.
\code{
defn floor-log2 (i:Int) -> Int
defn floor-log2 (i:Long) -> Long
}

\subsubheader{next-pow2}
\code{next-pow2} computes the smallest power of \code{2} that is greater than or equal to the given integer \code{i}. \code{i} cannot be negative.
\code{
defn next-pow2 (i:Int) -> Int
defn next-pow2 (i:Long) -> Long
}

\subsubheader{prev-pow2}
\code{prev-pow2} computes the largest power of \code{2} that is smaller than or equal to the given integer \code{i}. \code{i} cannot be negative.
\code{
defn prev-pow2 (i:Int) -> Int
defn prev-pow2 (i:Long) -> Long
}

\subsubheader{sum}
\code{sum} computes the sum of the given sequence of numbers.
\code{
defn sum (xs:Seqable<Int>) -> Int
defn sum (xs:Seqable<Long>) -> Long
defn sum (xs:Seqable<Float>) -> Float
defn sum (xs:Seqable<Double>) -> Double
}

\subsubheader{product}
\code{product} computes the product of the given sequence of numbers.
\code{
defn product (xs:Seqable<Int>) -> Int
defn product (xs:Seqable<Long>) -> Long
defn product (xs:Seqable<Float>) -> Float
defn product (xs:Seqable<Double>) -> Double
}

\subheader{Boolean Types}
Boolean values are represented using the types \code{True} and \code{False}.
\code{
deftype True <: Equalable
deftype False <: Equalable
}

Both \code{True} and \code{False} are subtypes of \code{Equalable} and hence support the \code{equal?} operation. Values of type \code{True} are only equal to other values of type \code{True}. Values of type \code{False} are only equal to other values of type \code{False}.

\subsubheader{complement}
\code{complement} returns the logical complement of its argument. If \code{a} is true, then it returns \code{false}, otherwise it returns \code{true}.
\code{
defn complement (a:True|False) -> True|False
}

\subheader{Character Type}   
A \code{Char} represents a single byte ascii character.
\code{
deftype Char <: Equalable & Hashable & Comparable<Char>
}

\code{Char} is defined to be a subtype of \code{Equalable}, \code{Hashable}, and \code{Comparable}, and thus it supports the equality operator,  the \code{hash} multi, and can be compared other values of type \code{Char}.

Two values of type \code{Char} are compared according to the numerical value of their ascii encoding.

\subsubheader{digit?}
\code{digit?} checks whether a given character is a digit character, representing one of the numerals between \code{0} and \code{9}.
\code{
defn digit? (c:Char) -> True|False
}

\subsubheader{letter?}
\code{letter?} checks whether a given character is either a lower or upper-case character, representing one of the letters between \code{'A'} and \code{'Z'} or between \code{'a'} and \code{'z'}.
\code{
defn letter? (c:Char) -> True|False
}

\subsubheader{upper-case?}
\code{upper-case?} checks whether a given character is an upper-case character, representing one of the letters between \code{'A'} and \code{'Z'}.
\code{
defn upper-case? (c:Char) -> True|False
}

\subsubheader{lower-case?}
\code{lower-case?} checks whether a given character is a lower-case character, representing one of the letters between \code{'a'} and \code{'z'}.
\code{
defn lower-case? (c:Char) -> True|False
}

\subsubheader{lower-case}
\code{lower-case} converts a given character to its lower-case representation if it is a letter, otherwise returns the original character.
\code{
defn lower-case (c:Char) -> Char
}

\subsubheader{upper-case}
\code{upper-case} converts a given character to its upper-case representation if it is a letter, otherwise returns the original character.
\code{
defn upper-case (c:Char) -> Char
}

\subheader{Range}
A \code{Range} represents a span of integers between a starting index and an optional ending index. 
\code{
deftype Range <: Collection<Int> & Equalable  
}

\subsubheader{Range}
The following function creates a range with a given starting index, an optional ending index, a step size, and a flag indicating whether the ending index is inclusive or exclusive.
\code{
defn Range (start:Int, end:Int|False, step:Int, inclusive?:True|False) -> Range
}

Users will typically use one of the macro forms for creating a range. The following form creates a range with the starting index \code{a}, exclusive ending index \code{b}, and step size \code{1}.
\code{
a to b 
}

The keyword \code{by} can be used to provide a custom step size.
\code{
a to b by n
}

To create ranges with inclusive ending indices, use the keyword \code{through} instead of \code{to}.
\code{
a through b
a through b by n
}

\code{Range} is a subtype of \code{Collection} and implements an appropriate method for the \code{to-seq} multi. A range is viewed as a sequence of numbers starting from its given starting index, and proceeding until its ending index by the given step size. If the ending index is inclusive, then the sequence may contain the ending index. If the ending index is exclusive, then the sequence will not contain the ending index. If no ending index is given, then the sequence is infinite.

\code{Range} is a subtype of \code{Equalable} and two ranges are defined to be equal if they have equivalent starting and ending indices, step sizes, and inclusion flags.

\subsubheader{Dense Index Ranges}
For many of the core library functions, ranges are used to specify bounds within a \code{Lengthable} collection. For these ranges, the range argument must be a \italic{dense index range} with respect to the given collection. A range is a \italic{dense index range} with respect to a collection if it satisfies the following restrictions:
\list{
\item The step size of the range must be 1. 
\item If the range is infinite, then the starting index must be non-negative and less than or equal to the length of the collection.
\item If the range is finite and inclusive, then the starting and ending indices must be non-negative and less than the length of the collection. The starting index must be less than or equal to the ending index.
\item If the range is finite and exclusive, then the starting and ending indices must be non-negative and less than or equal to the length of the collection. The starting index must be less than or equal to the ending index.
}

\subsubheader{start}
\code{start} returns the starting index of the given range.
\code{
defn start (r:Range) -> Int
}

\subsubheader{end}
\code{end} returns the ending index of the given range if there is one, or \code{false} otherwise.
\code{
defn end (r:Range) -> Int
}

\subsubheader{step}
\code{step} returns the step size of the given range.
\code{
defn step (r:Range) -> Int
}

\subsubheader{inclusive?}
\code{inclusive?} returns \code{true} if the ending index of the given range is inclusive, or \code{false} otherwise.
\code{
defn inclusive? (r:Range) -> True|False
}

\subsubheader{map}
\code{map} returns a list containing the result of applying the function \code{f} to each integer within the given range. It is a fatal error to call \code{map} on a range with infinite length.
\code{
defn map<?R> (f: Int -> ?R, r:Range) -> List<R>
}

\subheader{String}
A \code{String} represents an immutable collection of values of type \code{Char}.
\code{
deftype String <: Lengthable &
                  Collection<Char> &
                  Equalable &
                  Hashable &
                  Comparable<String>
}

\code{String} is a subtype of \code{Equalable} and hence supports the \code{equal?} operation. Two strings are equal if they have the same length and all of their characters are respectively equal.

\code{String} is a subtype of \code{Lengthable} and hence supports the \code{length} operation.

\code{String} is a subtype of \code{Hashable}, hence supports the \code{hash} operation and can be used as keys in a \code{HashTable}.

\code{String} is a subtype of \code{Comparable}, and two values of type \code{String} can be compared using the standard comparison operations according to their lexicographic ordering.

\code{String} is a subtype of \code{Collection}, and supports the \code{to-seq} operation for viewiewing the \code{String} as a sequence of values of type \code{Char}.

\subsubheader{String}

The following function creates a new \code{String} containing \code{n} copies of the \code{c} character.
\code{
defn String (n:Int, c:Char) -> String
}

The following function converts the given sequence of characters into a \code{String}.
\code{
defn String (cs:Seqable<Char>) -> String
}

\subsubheader{get}
\code{get} allows for retrieval of individual characters in a \code{String} by index. It is a fatal error to provide an index that is beyond the bounds of the string.
\code{
defn get (s:String, i:Int) -> Char
}

\subsubheader{get}
\code{get} allows for retrieval of a range of characters in a \code{String}. The given range must be a \italic{dense index range} with respect to the given string.
\code{
defn get (s:String, r:Range) -> String
}

For example, assuming that \code{s} contains the string \code{"Hello World"}, here is what various calls to \code{get} will return:
\code{
s[1 to 4]        returns   "ell"
s[1 through 4]   returns   "ello"
s[1 to false]    returns   "ello World"
}

\subsubheader{Parsing Numbers}
The following functions convert the string representations of numbers to their numerical types.
\code{
defn to-byte (s:String) -> False|Byte
defn to-int (s:String) -> False|Int
defn to-long (s:String) -> False|Long
defn to-float (s:String) -> False|Float
defn to-double (s:String) -> False|Double
}

Each of the above functions returns \code{false} if the given string cannot be converted to the appropriate type, either because of an incorrectly formatted string, or because the resulting number cannot be represented using the appropriate number of bits.

\subsubheader{to-string}
The following multi converts a given object to its string representation.
\code{
defmulti to-string (x) -> String
}

A default method defined in terms of \code{print} is provided to convert arbitrary objects to their string representation. User defined types are advised to rely upon this default method and \italic{not} provide their own method for \code{to-string}. To provide custom printing behaviour for a type, users should provide methods for \code{print} instead.  

\subsubheader{String Interpolation}
The \code{modulo} operator creates a printable object given a format string and a sequence of arguments.
\code{
defn modulo (format:String, args:Seqable) -> Printable
}

The characters in the format string are printed one by one, where occurrences of a splicing operator prints the next item in the sequence. The available splicing operators are:
\list{
\item \code{%_} : Prints the next item in the sequence.
\item \code{%*} : Assumes the next item in the sequence is \code{Seqable}, and prints all elements in the item.
\item \code{%,} : Assumes the next item in the sequence is \code{Seqable}, and prints all elements in the item separated by commas.
\item \code{%~} : Pretty prints the next item in the sequence using the \code{write} function.
\item \code{%@} : Assumes the next item in sequence is \code{Seqable}, and pretty prints all elements in the item separated by spaces.
\item \code{%%} : Prints the percentage sign character.
}

For example, the following command will print \code{"On Tuesdays, Thursdays, Wednesdays, Patrick goes for a walk."}.
\code{
val days = ["Tuesdays", "Thursdays", "Wednesdays"]
val name = "Patrick"
val activity = "walk"
println("On %,, %_ goes for a %_." % [days, name, activity])
}

Note that the \code{modulo} function does \italic{not} return a \code{String}. The \code{to-string} function may be used to convert the printable object into a string.
\code{
val days = ["Tuesdays", "Thursdays", "Wednesdays"]
val name = "Patrick"
val activity = "walk"
to-string("On %,, %_ goes for a %_." % [days, name, activity])
}

\subsubheader{matches?}
\code{matches?} returns \code{true} if the string \code{b} can be found at position \code{start} in the string \code{a}, or \code{false} otherwise.
\code{
defn matches? (a:String, start:Int, b:String) -> True|False
}

\subsubheader{prefix?}
\code{prefix?} returns \code{true} if the string \code{a} starts with the given \code{prefix}, or \code{false} otherwise.
\code{
defn prefix? (s:String, prefix:String) -> True|False
}

\subsubheader{suffix?}
\code{suffix?} returns \code{true} if the string \code{a} ends with the given suffix, or \code{false} otherwise.
\code{
defn suffix? (s:String, suffix:String) -> True|False
}

\subsubheader{empty?}
\code{empty?} returns \code{true} if the string contains no characters.
\code{
defn empty? (s:String) -> True|False
}

\subsubheader{append}
\code{append} returns a new string resulting from concatenating string \code{b} to the end of string \code{a}.
\code{
defn append (a:String, b:String) -> String
}

\subsubheader{append-all}
\code{append-all} returns a new string resulting from concatenating together all strings in a sequence.
\code{
defn append-all (xs:Seqable<String>) -> String
}

\subsubheader{string-join}
\code{string-join} returns the string resulting from printing out each item in the sequence \code{xs}. If the argument \code{j} is given, then each item in the sequence is separated by \code{j}.
\code{
defn string-join (xs:Seqable) -> String
defn string-join (xs:Seqable, j) -> String
}

\subsubheader{index-of-char}
\code{index-of-char} searches the string \code{s}, within the bounds indicated the range \code{r}, for the first occurrence of the character \code{c}. If \code{c} is found, then its index is returned, otherwise \code{false} is returned. If a range is not given, then by default the function searches through the entire string. The given range must be a \italic{dense index range} with respect to the string.
\code{
defn index-of-char (s:String, r:Range, c:Char) -> False|Int
defn index-of-char (s:String, c:Char) -> False|Int
}

\subsubheader{index-of-chars}
\code{index-of-chars} searches the string \code{s}, within the bounds indicated by the range \code{r}, for the first occurrence of the substring \code{b}. If it is found, then its index is returned, otherwise \code{false} is returned. If a range is not given, then by default the function searches through the entire string. The given range must be a \italic{dense index range} with respect to the string.
\code{
defn index-of-chars (s:String, r:Range, b:String) -> False|Int
defn index-of-chars (s:String, b:String) -> False|Int
}

\subsubheader{last-index-of-char}
\code{last-index-of-char} searches the string \code{s}, within the bounds indicated the range \code{r}, for the last occurrence of the character \code{c}. If \code{c} is found, then its index is returned, otherwise \code{false} is returned. If a range is not given, then by default the function searches through the entire string. The given range must be a \italic{dense index range} with respect to the string.
\code{
defn last-index-of-char (s:String, r:Range, c:Char) -> False|Int
defn last-index-of-char (s:String, c:Char) -> False|Int
}

\subsubheader{last-index-of-chars}
\code{last-index-of-chars} searches the string \code{s}, within the bounds indicated by the range \code{r}, for the last occurrence of the substring \code{b}. If it is found, then its index is returned, otherwise \code{false} is returned. If a range is not given, then by default the function searches through the entire string. The given range must be a \italic{dense index range} with respect to the string.
\code{
defn last-index-of-chars (s:String, r:Range, b:String) -> False|Int
defn last-index-of-chars (s:String, b:String) -> False|Int
}

\subsubheader{replace}
\code{replace} returns the result of replacing every occurrence of the character \code{c1} in the string \code{s} with the character \code{c2}.
\code{
defn replace (s:String, c1:Char, c2:Char) -> String
}

\subsubheader{replace}
\code{replace} returns the result of replacing every occurrence of the substring \code{s1} in the string \code{str} with the substring \code{s2}.
\code{
defn replace (str:String, s1:String, s2:String) -> String
}

\subsubheader{split}
\code{split} returns a lazily computed sequence resulting from splitting the string \code{str} at occurrences of the substring \code{s}. If the argument \code{n} is given, then the resulting sequence is restricted to contain at most \code{n} strings, where the last string contains the unsplit remainder of \code{str}.
\code{
defn split (str:String, s:String) -> Seq<String>   
defn split (str:String, s:String, n:Int) -> Seq<String>
}

The following relation is guaranteed return \code{true}.
\code{
string-join(split(str, s), s) == str
}

\subsubheader{lower-case}
\code{lower-case} the result of converting all letters contained within the string \code{s} to lower-case.
\code{
defn lower-case (s:String) -> String
}

\subsubheader{upper-case}
\code{upper-case} the result of converting all letters contained within the string \code{s} to upper-case.
\code{
defn upper-case (s:String) -> String
}

\subsubheader{trim}
\code{trim} returns the result of removing all leading and trailing characters that satisfy the function \code{pred} from the string \code{s}.
\code{
defn trim (pred: Char -> True|False, s:String) -> String
}

If \code{pred} is not given, then \code{trim}, by default, removes all leading and trailing whitespace characters.

\subheader{StringBuffer}
A \code{StringBuffer} is an extendable array for holding characters.
\code{
deftype StringBuffer <: IndexedCollection<Char> & OutputStream     
}

\code{StringBuffer} is a subtype of a \code{IndexedCollection} and implements appropriate methods for retrieving its length, and getting and setting characters by index.

When setting a character at an index \code{i}, if \code{i} is less than the length of the buffer, then the character currently at index \code{i} will be overwritten. If \code{i} is equal to the length of the buffer, then \code{i} will be added to the end of the buffer, and the buffer's length will increase by 1.

\code{StringBuffer} is a subtype of \code{OutputStream} and can be used as a target for the \code{print} function.

\subsubheader{StringBuffer}
This function creates a \code{StringBuffer} with the initial capacity \code{n}. The capacity of a StringBuffer is the number of characters it may hold before it undergoes resizing.
\code{
defn StringBuffer (n:Int) -> StringBuffer
}

If no capacity is provided, then the default capacity is \code{32}.
\code{
defn StringBuffer () -> StringBuffer                 
}

\subsubheader{add}
This function will add the character \code{c} to the end of the buffer.
\code{
defn add (s:StringBuffer, c:Char) -> False
}

\subsubheader{add-all}
This function will add all the characters in the sequence \code{cs} to the end of the buffer.
\code{
defn add-all (s:StringBuffer, cs:Seqable<Char>) -> False
}

\subsubheader{clear}
This function will clear all characters in the \code{StringBuffer}.
\code{
defn clear (s:StringBuffer) -> False
}

\subheader{Array}
Arrays are the most fundamental \code{IndexedCollection} in Stanza, representing a finite series of items with constant time access to elements by index.
\code{
deftype Array<T> <: IndexedCollection<T> & Equalable
}

\code{Array} is a subtype of \code{IndexedCollection} and hence supports the fundamental \code{get}, \code{set}, and \code{length} operations.

\code{Array} is a subtype of \code{Equalable} and two arrays are defined to be equal only if they are the same array.

\subsubheader{Array}
This function creates an \code{Array} with length \code{n} for holding values of type \code{T}. It is a fatal error to read from an index that has not been initialized, and \code{n} cannot be negative.
\code{
defn Array<T> (n:Int) -> Array<T>
}

This function creates an \code{Array} with length \code{n} for holding values of type \code{T} with each index initialized to the item \code{x}. \code{n} cannot be negative.
\code{
defn Array<T> (n:Int, x:T) -> Array<T>
}

\subsubheader{to-array}
This function converts a sequence of items of type \code{T} to an array.
\code{
defn to-array<T> (xs:Seqable<T>) -> Array<T>
}

\subsubheader{map}
This function creates a new array where each item is initialized to the result of calling the function \code{f} on each item in the given array \code{xs}. Note that the element type of the result array, \code{R}, must be provided explicitly.
\code{
defn map<R,?T> (f:T -> R, xs:Array<?T>) -> Array<R>
}

\subheader{CharArray}
\code{CharArray} objects are specialized arrays designed specifically for efficiently storing \code{Char} objects.
\code{
deftype CharArray <: Array<Char>
}

\code{CharArray} objects behave equivalently to \code{Array<Char>} objects.

\subsubheader{CharArray}
This function creates a \code{CharArray} of length \code{n}, with each index initialized to the space character.
\code{
defn CharArray (n:Int) -> CharArray
}

This function creates a \code{CharArray} of length \code{n}, with each index initialized to the given character \code{x}.
\code{
defn CharArray (n:Int, x:Char) -> CharArray
}

\subsubheader{get-chars}
This function allows for efficient retrieval of characters from a \code{CharArray}. The range \code{r} must be a \italic{dense index range} with respect to \code{cs}.
\code{
defn get-chars (cs:CharArray, r:Range) -> String
}
   
\subsubheader{set-chars}
This function allows for efficient assignment of characters in a \code{CharArray}. The range \code{r} must be a \italic{dense index range} with respect to \code{cs}.
\code{
defn set-chars (cs:CharArray, r:Range, s:String) -> False
}

\subheader{ByteArray}
\code{ByteArray} objects are specialized arrays designed specifically for efficiently storing \code{Byte} objects.
\code{
deftype ByteArray <: Array<Byte>
}

\code{ByteArray} objects behave equivalently to \code{Array<Byte>} objects.

\subsubheader{ByteArray}
This function creates a \code{ByteArray} of length \code{n}, with each index initialized to \code{0Y}.
\code{
defn ByteArray (n:Int) -> ByteArray
}

This function creates a \code{ByteArray} of length \code{n}, with each index initialized to the given byte \code{x}.
\code{
defn ByteArray (n:Int, x:Byte) -> ByteArray
}

\subheader{Tuple}
A \code{Tuple} represents a finite and immutable series of items each with an integer index.
\code{
deftype Tuple<T> <: Lengthable &
                    Collection<T> &
                    Equalable &
                    Hashable &
                    Comparable<Tuple<T>>
}

\code{Tuple} is a subtype of \code{Equalable} and two tuples are defined to be equal if they have the same length and all of their components are equal. It is an error to check whether two tuples are equal if they contain elements that are not \code{Equalable}.

\code{Tuple} is a subtype of \code{Collection} and implements an appropriate method for the \code{to-seq} multi to access the elements of the tuple as a sequence.

\code{Tuple} is a subtype of \code{Hashable} and computes its hash by combining the hashes of all of its items. It is a fatal error to call \code{hash} on a tuple containing items that are not \code{Hashable}.

\code{Tuple} is a subtype of \code{Comparable} and implements an appropriate method for the \code{compare} multi that compares two tuples by their lexicographic order. It is a fatal error to compare two tuples containing elements that are not \code{Comparable} with each other.

\code{Tuple} is a subtype of \code{Lengthable} and its length can be queried by calling the \code{length} multi.

\subsubheader{Tuple}
\code{Tuple} creates a tuple of length \code{n} with each element initialized to \code{x}.
\code{
defn Tuple<?T> (n:Int, x:?T) -> Tuple<T>              
}

\subsubheader{to-tuple}
\code{to-tuple} converts a finite sequence of items into a tuple. The function will not terminate if given an infinite sequence.
\code{
defn to-tuple<?T> (xs:Seqable<?T>) -> Tuple<T>
}

\subsubheader{get}
\code{get} will return the item at index \code{i} within the tuple \code{x}. \code{i} must be within the bounds of the range.
\code{
defn get<?T> (x:Tuple<?T>, i:Int) -> T
}

\subsubheader{get}
\code{get} will return a new \code{Tuple} containing the items spanned by the given range. The range must be a \italic{dense index range} with respect to the tuple. 
\code{
defn get<?T> (xs:Tuple<?T>, r:Range) -> Tuple<T>
}

\subsubheader{map}
\code{map} returns a new tuple containing the result of calling the function \code{f} on each item in the tuple \code{xs}.
\code{
defn map<?T,?R> (f:T -> ?R, xs:Tuple<?T>) -> Tuple<R>
}

\subsubheader{empty?}
\code{empty?} returns \code{true} if the tuple \code{t} is empty, otherwise it returns \code{false}.
\code{
defn empty? (t:Tuple) -> True|False
}

\subheader{List}
A List represents an immutable linked list of items.
\code{
deftype List<T> <: Lengthable &
                   Collection<T> &
                   Equalable &
                   Hashable &
                   Comparable<List<T>>   
}

\code{List} is a subtype of \code{Lengthable} and its length can be queried by calling the \code{length} multi. Note that the length of a list is not cached, and computing the length of a list takes time proportional to the length of the list.

\code{List} is a subtype of \code{Collection} and implements an appropriate method for the \code{to-seq} multi for viewing the list as a sequence.

\code{List} is a subtype of \code{Equalable} and two lists are defined to be equal if they have the same length and all of their components are equal. It is a fatal error to check whether two lists are equal if they contain elements that are not \code{Equalable}.

\code{List} is a subtype of \code{Hashable} and implements an appropriate method for the \code{hash} multi. The hash is computed by combining the hashes of all the items in the list. It is a fatal error to call hash on a list containing elements that are not \code{Hashable}.

\code{List} is a subtype of \code{Comparable} and implements an appropriate method for the \code{compare} multi. Two lists are compared according to their lexicographic ordering. It is a fatal error to compare two lists that contain elements that are not \code{Comparable} to each other.

\subsubheader{List}
The following functions create lists with no, one, two, three, or four elements respectively. The \code{to-list} function can be used to create lists with more than four elements.
\code{
defn List () -> List<Void>
defn List<?T> (x:?T) -> List<T>
defn List<?T> (x:?T, y:?T) -> List<T>
defn List<?T> (x:?T, y:?T, z:?T) -> List<T>
defn List<?T> (w:?T, x:?T, y:?T, z:?T) -> List<T>
}

\subsubheader{cons}
\code{cons} (short for "construct") creates new lists by appending one, two, or three elements to the head of an existing list. The \code{append} function can be used to append more than three elements to a list.
\code{
defn cons<?T> (x:?T, t:List<?T>) -> List<T>
defn cons<?T> (x:?T, y:?T, t:List<?T>) -> List<T>
defn cons<?T> (x:?T, y:?T, z:?T, t:List<?T>) -> List<T>
}

\subsubheader{to-list}
\code{to-list} creates a new list containing the items in the sequence \code{xs}. The function will not terminate if given an infinite sequence.
\code{
defn to-list<?T> (xs:Seqable<?T>) -> List<T>
}

\subsubheader{head}
\code{head} returns the head of a list. It is a fatal error to call \code{head} on an empty list.
\code{
defmulti head<?T> (x:List<?T>) -> T
}

\subsubheader{tail}
\code{tail} returns the tail of a list. It is a fatal error to call \code{tail} on an empty list.
\code{
defmulti tail<?T> (x:List<?T>) -> List<T>
}

\subsubheader{empty?}
\code{empty?} returns \code{true} if the given list is empty or \code{false} otherwise.
\code{
defmulti empty? (x:List) -> True|False
}

\subsubheader{get}
\code{get} returns the \code{i}'th element in the list \code{xs}. \code{i} must be within the bounds of \code{xs}.
\code{
defn get<?T> (xs:List<?T>, i:Int) -> T
}

\subsubheader{headn}
\code{headn} returns a list containing the \code{n} first elements in the list \code{l}. \code{n} must be less than or equal to the length of the list.
\code{
defn headn<?T> (l:List<?T>, n:Int) -> List<T>
}

\subsubheader{tailn}
\code{tailn} returns the list after removing the first \code{n} elements from the head of the list \code{l}. \code{n} must be less than or equal to the length of the list.
\code{
defn tailn<?T> (l:List<?T>, n:Int) -> List<T>
}

\subsubheader{reverse}
\code{reverse} returns a new list containing the items in \code{xs} in reversed order.
\code{
defn reverse<?T> (xs:List<?T>) -> List<T>
}

\subsubheader{in-reverse}
\code{in-reverse} returns a sequence containing the items in the list in reverse order.
\code{
defn in-reverse<?T> (xs:List<?T>) -> Seq<T>
}

\subsubheader{last}
\code{last} returns the last element in the list. The list must not be empty.
\code{
defn last<?T> (xs:List<?T>) -> T
}

\subsubheader{but-last}
\code{but-last} returns a list containing all elements in \code{xs} except the last. \code{xs} must not be empty.
\code{
defn but-last<?T> (xs:List<?T>) -> List<T>
}

\subsubheader{append}
\code{append} returns the list resulting from appending all items from \code{xs} to the beginning of the list \code{ys}.
\code{
defn append<?T> (xs:Seqable<?T>, ys:List<?T>) -> List<T>
}

\subsubheader{append-all}
\code{append-all} returns the list resulting from appending together all lists in the sequence \code{xs}.
\code{
defn append-all<?T> (xs:Seqable<List<?T>>) -> List<T>
}

\subsubheader{transpose}
\code{transpose} returns a transposed version of the input list \code{xs}. The first element in the result list is a list containing the first elements in each list within \code{xs}. The second element in the result list is a list containing the second elements in each list within \code{xs}. The third element in the result list is a list containing the third elements in each list within \code{xs}, et cetera. The result list has the same length as the shortest list within xs.
\code{
defn transpose<?T> (xs:List<List<?T>>) -> List<List<T>>
}

\subsubheader{map}
\code{map} returns a new list consisting of the results of applying the function \code{f} to each element in the input list \code{xs}.
\code{
defn map<?T,?R> (f: T -> ?R, xs:List<?T>) -> List<R>
}

\subsubheader{map}
The following functions returns a new list consisting of the results of applying the function \code{f} to each pair (or triplet) of elements in the given input lists (or sequences). The result list has the same length as the shortest input list or sequence.
\code{
defn map<?T,?S,?R> (f: (T,S) -> ?R,
                    xs:List<?T>,
                    ys:Seqable<?S>) -> List<R>
                    
defn map<?T,?S,?U,?R> (f: (T,S,U) -> ?R,
                       xs:List<?T>,
                       ys:Seqable<?S>,
                       zs:Seqable<?U>) -> List<R>
}

\subsubheader{seq-append}
\code{seq-append} returns a new list by calling the function \code{f} on every item in the sequence \code{xs}, and then appending together all of the resultant lists.
\code{
defn seq-append<?T,?R> (f: T -> List<?R>, xs:Seqable<?T>) -> List<R>
}

\subsubheader{seq-append}
The following functions returns a new list by calling the function \code{f} on each pair (or triplet) of items in the input sequences, and then appending together all of the resultant lists.
\code{
defn seq-append<?T,?S,?R> (f: (T,S) -> List<?R>,
                           xs:Seqable<?T>,
                           ys:Seqable<?S>) -> List<R>
                           
defn seq-append<?T,?S,?U,?R> (f: (T,S,U) -> List<?R>,
                              xs:Seqable<?T>,
                              ys:Seqable<?S>,
                              zs:Seqable<?U>) -> List<R>
}

\subheader{FileInfo}
A \code{FileInfo} object contains position information about a specific point in a text file.
\code{
deftype FileInfo <: Equalable &
                    Hashable &
                    Comparable<FileInfo>
}

\code{FileInfo} is a subtype of \code{Equalable} and two \code{FileInfo} objects are equal when their filenames, lines, and columns are equal.

\code{FileInfo} is a subtype of \code{Hashable} and implements an appropriate method for the \code{hash} multi by combining the hashes of its filename, line, and column information.

\code{FileInfo} is a subtype of \code{Comparable} and implements an appropriate method for the \code{compare} multi. Two \code{FileInfo} objects are compared by ordering their filenames, followed their line index, followed by their column index.

\subsubheader{FileInfo}
\code{FileInfo} creates a \code{FileInfo} from the given filename, line, and column information.
\code{
defn FileInfo (f:String, l:Int, c:Int) -> FileInfo
}

\subsubheader{filename}
\code{filename} retrieves the filename of the given \code{FileInfo} object.
\code{
defn filename (i:FileInfo) -> String
}

\subsubheader{line}
\code{line} retrieves the line number of the given \code{FileInfo} object.
\code{
defn line (i:FileInfo) -> Int
}

\subsubheader{column}
\code{column} retrieves the column number of the given \code{FileInfo} object.
\code{
defn column (i:FileInfo) -> Int
}

\subheader{Token}
A \code{Token} represents a value that has been associated with a \code{FileInfo} object indicating the position from which it originated in a text file.
\code{
deftype Token <: Equalable
}

A \code{Token} is a subtype of \code{Equalable} and two tokens are considered equal if its \code{FileInfo} objects are equal and the wrapped values are equal. It is a fatal error to check whether two \code{Token} objects are equal if their wrapped values are not \code{Equalable}.

\subsubheader{Token}
This function creates a \code{Token} from the given value and its associated \code{FileInfo} object.
\code{
defn Token (item, info:FileInfo) -> Token
}

\subsubheader{item}
This function retrieves the wrapped value in a \code{Token} object.
\code{
defn item (t:Token) -> ?
}

\subsubheader{info}
This function retrieves the associated FileInfo object in a \code{Token} object.
\code{
defn info (t:Token) -> FileInfo
}

\subsubheader{unwrap-token}
This function returns the possibly wrapped value in a token by recursively unwrapping the value if it is a token.
\code{
defn unwrap-token (t) -> ?
}

\subsubheader{unwrap-all}
This function recursively calls \code{unwrap-token} on all tokens and lists in the given value. This function is commonly used to strip away all file information from an s-expression.
\code{
defn unwrap-all (t) -> ?
}

\subheader{KeyValue}
A \code{KeyValue} pair represents a pairing of a key object of type \code{K} with a value object of type \code{V}. It is typically used to represent entries in datastructures where values are looked up according to a given key.
\code{
deftype KeyValue<K,V> 
}

Typically, users will use the following macro form for creating KeyValue pairs.
\code{
k => v
}

\subsubheader{KeyValue}
This function creates a \code{KeyValue} pair from the given key and value objects.
\code{
defn KeyValue<?K,?V> (k:?K, v:?V) -> KeyValue<K,V>
}

\subsubheader{key}
This function retrieves the key object in a \code{KeyValue} object.
\code{
defn key<?K> (kv:KeyValue<?K,?>) -> K
}

\subsubheader{value}
This function retrieves the value object in a \code{KeyValue} object.
\code{
defn value<?V> (kv:KeyValue<?,?V>) -> V
}

\subheader{Symbols}
\code{Symbols} are used to represent identifiers in s-expressions, and can be compared against each other efficiently and used as keys in hashtables.
\code{
deftype Symbol <: Equalable & Hashable
}

Symbols are divided into two categories: symbols created from strings (\code{StringSymbol}), and uniquely generated symbols (\code{GenSymbol}).
\code{
deftype StringSymbol <: Symbol
deftype GenSymbol <: Symbol
}

\code{Symbol} is a subtype of \code{Equalable} and two symbols can be efficiently compared to see whether they are equal. Two symbols created from strings are equal if the strings they are created from are equal. Two uniquely generated symbols are equal if and only if they were created by the same call to \code{gensym}.

\code{Symbol} is a subtype of \code{Hashable} and implements an appropriate method for the \code{hash} multi. For symbols constructed from strings, the hash is computed from its name. For generated symbols, the hash is computed from its id.

\subsubheader{to-symbol}
This function constructs a symbol from the given value by converting it to a string using \code{to-string} and then creating a symbol from the resulting string. If \code{x} is already a symbol then it is returned directly.
\code{
defn to-symbol (x) -> Symbol
}

\subsubheader{symbol-join}
This function constructs a symbol from the given sequence by converting it to a string using \code{string-join} and then creating a symbol from the resulting string.
\code{
defn symbol-join (xs:Seqable) -> Symbol
}

\subsubheader{gensym}
This function generates a unique symbol given an object \code{x} whose string representation is used as its name. The resulting symbol is guaranteed to be equal to no other symbol currently live in the program.
\code{
defn gensym (x) -> Symbol
}

If no object \code{x} is provided, then the default name is the string "$gen".
\code{
defn gensym () -> Symbol
}

\subsubheader{name}
This function retrieves the string that the symbol was constructed with.
\code{
defn name (x:Symbol) -> String
}

\subsubheader{id}
This function retrieves the identifier associated with a generated symbol. Every generated symbol is guaranteed to be associated with a unique identifier.
\code{
defn id (x:GenSymbol) -> Int
}

\subsubheader{qualified?}
A qualified symbol is a symbol created from a string containing a \code{'/'} character. In Stanza, qualified symbols are used to indicate a package qualified identifier. The following function returns \code{true} if the given symbol is a qualified symbol, or \code{false} otherwise.
\code{
defn qualified? (a:Symbol) -> True|False
}

\subsubheader{qualifier}   
This function splits a qualified symbol into a 2 element tuple containing its qualifier and its unqualified name. If the argument is not a qualified symbol, then the qualifier is \code{false}.
\code{
defn qualifier (a:Symbol) -> [False|Symbol, Symbol]
}

\subsubheader{String Representation}
Note that two unique symbols, which are not equal to each other, may still have equivalent string representations. For example, a generated symbol with the name \code{"x"} and the id \code{253} has the same string representation as the symbol created from the string \code{"x253"}. 

A typical method of converting a list of unique symbols into a list of unique strings is the following:
\list{
\item For \code{StringSymbol} objects, directly use their name as their string representation.
\item For \code{GenSymbol} objects, choose a name that is guaranteed not to be a prefix of any \code{StringSymbol}, then append the name to the id of the symbol. This name is typically chosen in an application-specific manner.
}

\subheader{Maybe}
A \code{Maybe} object is used to indicate the presence or absence of an object.
\code{
deftype Maybe<T> <: Equalable & Comparable<Maybe<T>>
}

The \code{One} subtype of \code{Maybe} indicates the presence of an object of type \code{T}.
\code{
deftype One<T> <: Maybe<T>
}

The \code{None} subtype of \code{Maybe} indicates the absence of an object.
\code{
deftype None <: Maybe<Void>
}

\code{Maybe} is a subtype of \code{Equalable}. Two \code{None} objects are always equal. Two \code{One} objects are equal if their wrapped values are equal. It is an error to check whether two \code{Maybe} objects are equal if they wrap over values that are not \code{Equalable}.

\code{Maybe} is a subtype of \code{Comparable} and implements an appropriate method for the \code{compare} multi. \code{None} objects are always less than \code{One} objects. Two \code{One} objects are compared by comparing their wrapped values. It is an error to compare two \code{Maybe} objects if they wrap over values that are not \code{Comparable}.

\subsubheader{One}
This function wraps up the value \code{x} in a \code{One} object.
\code{
defn One<?T> (x:?T) -> One<T>
}

\subsubheader{None}
This function creates a new \code{None} object.
\code{
defn None () -> None
}

\subsubheader{value}
This function will retrieve the wrapped value in a \code{One} object.
\code{
defn value<?T> (x:One<?T>) -> T
}

\subsubheader{value?}
This function will retrieve the wrapped value in a \code{Maybe} object if one exists, otherwise it returns the \code{default} value.
\code{
defn value?<?T> (x:Maybe<?T>, default:?T) -> T
}

If no default value is given, then the \code{default} value is \code{false}.
\code{
defn value?<?T> (x:Maybe<?T>) -> T|False
}

\subsubheader{value!}
This function assumes that the given \code{Maybe} object is a \code{One} object and returns its wrapped value.
\code{
defn value!<?T> (x:Maybe<?T>) -> T
}

\subsubheader{empty?}
This function returns \code{true} if the given \code{Maybe} object contains no wrapped value (i.e. it is a \code{None} object).
\code{
defn empty? (x:Maybe) -> True|False
}

\subheader{Exception}
An \code{Exception} object is used to represent the conditions under which an exceptional behaviour as occurred.
\code{
deftype Exception  
}

\subsubheader{Exception}
Users are encouraged to create their own subtypes of \code{Exception} to represent different types of exceptional behaviour. However, the following function may be used to create a generic \code{Exception} object with the given error message.
\code{
defn Exception (msg) -> Exception
}

\subsubheader{throw}
This function throws the given exception object to the current exception handler. The return type of \code{Void} indicates that \code{throw} never returns.
\code{
defn throw (e:Exception) -> Void
}

\subsubheader{with-exception-handler}
This function runs the \code{body} function after installing the \code{handler} function as the current exception handler. If the body runs without throwing an exception, then its result is returned. Otherwise the result of the exception handler is returned.
\code{
defn with-exception-handler<?T> (body: () -> ?T, handler: Exception -> ?T) -> T
}

\subsubheader{with-finally}
This function runs the \code{body} function ensuring that the given \code{finally} function is run immediately afterwards. If the body runs to completion, then its result is returned after running the finally function. Otherwise, the finally function is run before execution exits the scope. 
\code{
defn with-finally<?T> (body: () -> ?T, finally: () -> ?) -> T
}

\subsubheader{try-catch-finally}
Users will not typically call \code{with-exception-handler} or \code{with-finally} directly and instead use the standard macro forms. The following form runs the given body with a handler for catching \code{MyException} objects. The code within the optional finally block is ensured to run whether or not the body runs to completion.
\code{
try :
   body code
catch (e:MyException) :
   exception handling code
finally :
   finalization code
}

\subheader{Fatal Errors}

A fatal error occurs when a program somehow enters an illegal state that indicates an error has occurred in the program. It is impossible for a program to recover from a fatal error, and thus fatal errors do not occur in correct programs.

\subsubheader{fatal}
This function will print out the given error message to the screen, display a stack trace allowing users to find the source of the error, and immediately terminate the program.
\code{
defn fatal (msg) -> Void
}

On the highest (and unsafe) optimization settings, Stanza assumes that your program is correct and hence no calls to \code{fatal} ever occurs. For example, on the highest optimization settings, the following code:
\code{
if n < 0 :
   fatal("n should not be negative")
else :
   f()
}
is allowed to be optimized to the following:
\code{
f()
}
as Stanza assumes that your program is correct, and therefore it is impossible for execution to enter the consequent branch of the if expression.

\subheader{Attempt and Failure}
The attempt and failure functions provide a convenient non-local exit for users.

\subsubheader{fail}
The \code{fail} function exits from the current attempt scope and calls the current failure handler.
\code{
defn fail () -> Void
}

\subsubheader{with-attempt}
This function calls the \code{conseq} function after installing the \code{alt} function as the current failure handler. If the \code{conseq} function runs to completion then its result is returned. Otherwise, if \code{fail} is called during execution of \code{conseq}, the \code{alt} function is called and its result is returned.
\code{
defn with-attempt<?T> (conseq: () -> ?T, alt: () -> ?T) -> T
}

\subsubheader{attempt-else}
Users will not typically call \code{with-attempt} directly and instead use the standard macro forms. The following form runs the given body with a failure handler that executes when fail is called.
\code{
attempt :
   body code
else :
   failure code
}
If no \code{else} branch is provided, then a default \code{else} branch is provided that simply returns \code{false}.

\subheader{Labeled Scopes}
Labeled scopes provide the ability to exit early from a block of code.

\subsubheader{LabeledScope}
This function executes the \code{thunk} function in a new labeled scope. The \code{thunk} function is passed an exit function that, when called, will immediately exit \code{thunk}. If the \code{thunk} runs to completion without ever calling the exit function then its result is returned. If the exit function is called during execution of \code{thunk} then the argument passed to the exit function is returned.
\code{
defn LabeledScope<T> (thunk: (T -> Void) -> T) -> T
}

\subsubheader{label}
Users will typically use the standard macro form for creating labeled scopes instead of directly calling \code{LabeledScope}. The following code will return the first prime number between \code{100} and \code{200}.
\code{
label<Int> return :
   for i in 100 to 200 do :
      if prime?(i) :
         return(i)
   fatal("No prime found!")      
}

\subheader{Generators}
Generators provide the ability to execute a function in a new coroutine and return its results in the form of a sequence. 

\subsubheader{Generator}
This function executes the \code{thunk} function in a new coroutine and returns a sequence. The \code{thunk} function is passed two functions as arguments. The first argument is the yield function, which takes a single argument. During execution of \code{thunk}, calls to the yield function will suspend the coroutine, and its argument will be included in the result sequence. The second argument is the \code{break} function, which takes either zero or one argument. During execution of \code{thunk}, a call to the break function with no arguments will immediately close the coroutine and end the result sequence. A call to the break function with one argument will immediately close the coroutine, include the argument as the last item in the result sequence, and then end the result sequence.
\code{
defn Generator<T> (thunk : (T -> False,
                            (T -> Void)&(() -> Void)) -> ?) -> Seq<T>
}

\subsubheader{generate}
Users will typically use the standard macro form for creating generators instead of directly calling \code{Generator}. The following code creates a sequence containing the first \code{100} prime numbers.
\code{
generate<Int> :
   var n = 0
   for i in 2 to false do :
      if prime?(i) :
         n = n + 1
         if n == 100 : break(i)
         else : yield(i)
}

\subheader{Coroutine}
Coroutines provide the ability to execute a function in a context that can be saved and resumed later. This ability is most often used to execute a piece of code concurrently with the main program. A coroutine for which objects of type \code{I} are sent to its wrapped function, and for which objects of type \code{O} are sent back from its wrapped function is represented by the following type.
\code{
deftype Coroutine<I,O> <: Equalable
}

\code{Coroutine} is a subtype of \code{Equalable}. Two coroutines are equal if they were created by the same call to \code{Coroutine}.

\subsubheader{Coroutine}
This function creates a coroutine for which objects of type \code{I} are sent to its wrapped function, and for which objects of type \code{O} are sent back from its wrapped function. The argument represents the wrapped function of the coroutine.
\code{
defn Coroutine<I,O> (enter: (Coroutine<I,O>, I) -> O) -> Coroutine<I,O>
}

The first argument to the wrapped function is the coroutine that is created. The second argument to the wrapped function is the argument passed in the first call to \code{resume} on the coroutine. The result of the function \code{enter} is the last value sent back from the wrapped function.

\subsubheader{resume}
This function transfers control into a coroutine, and sends it the value \code{x}. The return value of \code{resume} is the value that is sent back by the coroutine.
\code{
defn resume<?I,?O> (c:Coroutine<?I,?O>, x:I) -> O
}

\subsubheader{suspend}
This function transfers control out of a coroutine, and back to the main program. The value \code{x} is sent back to the main program. The return value of \code{suspend} is the value that is sent to the coroutine by the next call to \code{resume}.
\code{
defn suspend<?I,?O> (c:Coroutine<?I,?O>, x:O) -> I
}

\subsubheader{break}
This function transfers control flow out of a coroutine, back to the main program, and closes the coroutine. The value \code{x} is sent back to the main program. \code{break} has no return value as the coroutine is closed and cannot be resumed afterwards.
\code{
defmulti break<?O> (c:Coroutine<?,?O>, x:O) -> Void
}

\subsubheader{close}
This function closes a coroutine, and disallows any further calls to \code{resume} on the coroutine.
\code{
defn close (c:Coroutine) -> False
}

\subsubheader{active?}
This function returns \code{true} if the given coroutine is currently running. Only active coroutines can be suspended or broken from.
\code{
defn active? (c:Coroutine) -> True|False
}

\subsubheader{open?}
This function returns \code{true} if the given coroutine is not currently running and open to be resumed. Only open coroutines can be resumed.
\code{
defn open? (c:Coroutine) -> True|False
}

\subheader{Dynamic Wind}
Consider the following code which attempts to ensure that the global variable \code{X} is set to the value \code{42} while the function \code{f} is running, and for \code{X} to be restored after \code{f} is done.
\code{
val old-X = X
X = 42
f()
X = old-X
}

However, because of coroutines it is possible for the above code to finish during the call to \code{f}, (for example, if \code{f} throws an exception) and thus leave \code{X} unrestored. The \code{dynamic-wind} function solves this problem by allowing users to perform operations upon entering or leaving a scope.

\subsubheader{dynamic-wind}
\code{dynamic-wind} takes a \code{body} function accompanied by an optional entering function, \code{in}, and an exiting function, \code{out}. \code{dynamic-wind} performs a call to \code{body} while ensuring that \code{in} is called every time execution enters the scope of \code{body}, and that \code{out} is called every time execution exits the scope of \code{body}. 
\code{
defn dynamic-wind<?T> (in:False|(() -> ?),
                       body:() -> ?T,
                       out:False|(() -> ?)) -> T
}

Using \code{dynamic-wind}, the above example can be rewritten as follows:
\code{
val old-X = X
dynamic-wind(
   fn () : X = 42
   f
   fn () : X = old-X)
}

\subheader{Sequence Library}
The core package contains a large number of convenience functions that operate on sequences. The majority of collection datastructures in Stanza's core library are subtypes of \code{Seqable} and thus can be used with the sequence library.

\subsubheader{Operating Functions}
Operating functions, such as \code{do}, \code{seq}, and \code{find}, are functions with type signatures compatible with the \code{for} macro form. The following macro form:
\code{
for x in xs do :
   body code
}
is equivalent to the following direct call to the \code{do} operating function:
\code{
do(fn (x) :
      body code
   xs)
}

Multi-argument operating functions are used with the following macro form.
\code{
for (x in xs, y in ys, z in zs) do :
   body code
}
is equivalent to the following direct call to the \code{do} operating function:
\code{
do(fn (x, y, z) :
      body code
   xs, ys, zs)   
}

As a matter of style, if the body consists of more than a single function call then the macro form is used, as in the following example:
\code{
val x = 671
val prime? = for i in 2 to x none? :
   x % i == 0
}

If the body consists of a single function call then the standard function call form is used instead.
\code{
val x = 671
defn divides-x? (i:Int) : x % i == 0
val prime? = none?(divides-x?, 2 to x)
}

The above example can also be written using anonymous function notation as follows:
\code{\#{
val x = 671
val prime? = none?({x % _ == 0}, 2 to x)
\#}}

\subsubheader{do}
This multi calls the function \code{f} on each item in the sequence \code{xs}. A default method defined in terms of the fundamental sequence operations is provided, but users may provide customized methods for subtypes of \code{Seqable} for efficiency.
\code{
defmulti do<?T> (f:T -> ?, xs:Seqable<?T>) -> False
}

These multis operate similarly to the single collection version of \code{do} but instead calls the function \code{f} with successive items from multiple sequences. The sequences are stepped through in parallel and iteration stops as soon as one of them is empty.
\code{
defmulti do<?T,?S> (f:(T,S) -> ?,
                    xs:Seqable<?T>,
                    ys:Seqable<?S>) -> False

defmulti do<?T,?S,?U> (f:(T,S,U) -> ?,
                       xs:Seqable<?T>,
                       ys:Seqable<?S>,
                       zs:Seqable<?U>) -> False
}

\subsubheader{find}
This function iterates through the sequence \code{xs} and calls the function \code{f} on each item, searching for the first item for which \code{f} returns \code{true}. If \code{f} returns \code{true} on some item, then that item is returned by \code{find}. If \code{f} does not return \code{true} on any item in the sequence, then \code{find} returns \code{false}.
\code{
defn find<?T> (f: T -> True|False, xs:Seqable<?T>) -> T|False
}

This function iterates through the sequence \code{xs} and \code{ys} in parallel and calls the function \code{f} with an item from each sequence. Iteration stops as soon as either sequence is empty. If \code{f} returns \code{true} when called by an item, \code{x}, from \code{xs}, and an item, \code{y}, from \code{ys}, then \code{x} is returned by \code{find}.
\code{
defn find<?T,?S> (f: (T,S) -> True|False,
                  xs:Seqable<?T>,
                  ys:Seqable<?S>) -> T|False
}

\subsubheader{find!}
These functions behave identically to \code{find} except that they assume that there exists an item (or pair of items) for which \code{f} returns \code{true}.
\code{
defn find!<?T> (f: T -> True|False, xs:Seqable<?T>) -> T

defn find!<?T, ?S> (f: (T,S) -> True|False,
                    xs:Seqable<?T>,
                    ys:Seqable<?S>) -> T
}

\subsubheader{first}
This function iterates through the sequence \code{xs} and calls the function \code{f} on each item, searching for the first item for which \code{f} returns a \code{One} object. If \code{f} returns a \code{One} object on some item, then the \code{One} object is returned. If \code{f} returns a \code{None} object for all items in the sequence, then a \code{None} object is returned by \code{first}.
\code{
defn first<?T,?R> (f: T -> Maybe<?R>, xs:Seqable<?T>) -> Maybe<R>
}

This function iterates through the sequence \code{xs} and \code{ys} in parallel and calls the function \code{f} repeatedly with an item from each sequence. Iteration stops as soon as either sequence is empty. If \code{f} returns a \code{One} object on some pair of items, then the \code{One} object is returned. If \code{f} returns a \code{None} object for all items in the sequences, then a \code{None} object is returned by \code{first}.
\code{
defn first<?T,?S,?R> (f: (T,S) -> Maybe<?R>,
                      xs:Seqable<?T>,
                      ys:Seqable<?S>) -> Maybe<R>
}

\subsubheader{first!}
These functions behave identically to \code{first} except that they assume that there exists an item (or pair of items) for which \code{f} returns a \code{One} object. The wrapped value within the \code{One} object is returned.
\code{
defn first!<?T,?R> (f: T -> Maybe<?R>, xs:Seqable<?T>) -> R

defn first!<?T,?S,?R> (f: (T,S) -> Maybe<?R>,
                       xs:Seqable<?T>,
                       ys:Seqable<?S>) -> R
}

\subsubheader{seq}
This function constructs the sequence resulting from calling the function \code{f} on each item in the sequence \code{xs}.
\code{
defn seq<?T,?S> (f:T -> ?S, xs:Seqable<?T>) -> Seq<S>
}

These functions behave similarly to the single collection version of \code{seq} but instead calls the function \code{f} with successive items from multiple sequences. The sequences are stepped through in parallel and iteration stops as soon as one of them is empty.
\code{
defn seq<?T,?S,?R> (f:(T,S) -> ?R,
                    xs:Seqable<?T>,
                    ys:Seqable<?S>) -> Seq<R>

defn seq<?T,?S,?U,?R> (f:(T,S,U) -> ?R,
                       xs:Seqable<?T>,
                       ys:Seqable<?S>,
                       zs:Seqable<?U>) -> Seq<R>
}

\subsubheader{seq?}
This function constructs a sequence by calling the function \code{f} on each item in the sequence \code{xs}. For each item in \code{xs}, if \code{f} returns a \code{One} object, then the unwrapped value is included in the result sequence. If \code{f} returns a \code{None} object then the item is not included in the result sequence.
\code{
defn seq?<?T,?R> (f: T -> Maybe<?R>, xs:Seqable<?T>) -> Seq<R>
}

These functions construct a sequence by iterating through the given sequences in parallel and calling the function \code{f} repeatedly with an item from each sequence. For each pair (or triplet) of items, if \code{f} returns a \code{One} object, then the unwrapped value is included in the result sequence. If \code{f} returns a \code{None} object then the item is not included in the result sequence.
\code{
defn seq?<?T,?S,?R> (f: (T,S) -> Maybe<?R>,
                     xs:Seqable<?T>,
                     ys:Seqable<?S>) -> Seq<R>
                     
defn seq?<?T,?S,?U,?R> (f: (T,S,U) -> Maybe<?R>,
                        xs:Seqable<?T>,
                        ys:Seqable<?S>,
                        zs:Seqable<?U>) -> Seq<R>
}

\subsubheader{filter}
This function constructs a sequence by calling the function \code{f} on each item in the sequence \code{xs}, and including the item in the result sequence only if the call to \code{f} returns true.
\code{
defn filter<?T> (f: T -> True|False, xs:Seqable<?T>) -> Seq<T>
}

This function constructs a sequence by iterating through the \code{xs} and \code{ys} sequences in parallel and calling the function \code{f} repeatedly with an item from each sequence. For each pair of items, if \code{f} returns \code{true}, then the item from the \code{xs} sequence is included in the result sequence.
\code{
defn filter<?T,?S> (f: (T,S) -> True|False,
                    xs:Seqable<?T>,
                    ys:Seqable<?S>) -> Seq<T>
}

\subsubheader{index-when}
This function iterates through the items in the \code{xs} sequence, calling \code{f} on each one, and returns the first index at which the call to \code{f} returns \code{true}. If no call to \code{f} returns \code{true}, then \code{index-when} returns \code{false}.
\code{
defn index-when<?T> (f: T -> True|False, xs:Seqable<?T>) -> Int|False
}

This function iterates through the \code{xs} and \code{ys} sequences in parallel, calling \code{f} on each pair of items from \code{xs} and \code{ys}, and returns the first index at which the call to \code{f} returns \code{true}. If no call to \code{f} returns \code{true}, then \code{index-when} returns \code{false}.
\code{
defn index-when<?T,?S> (f: (T,S) -> True|False,
                        xs:Seqable<?T>,
                        ys:Seqable<?S>) -> Int|False
}

\subsubheader{index-when!}
These functions behave identically to \code{index-when} except that they assume there exists an item (or pair of items) for which the call to \code{f} returns \code{true}.
\code{
defn index-when!<?T> (f: T -> True|False, xs:Seqable<?T>) -> Int
                      
defn index-when!<?T,?S> (f: (T,S) -> True|False,
                         xs:Seqable<?T>,
                         ys:Seqable<?S>) -> Int
}

\subsubheader{split!}
This function iterates through the items in the \code{xs} sequence, separating them into two collections depending on whether calling \code{f} on the item returns \code{true} or \code{false}. The function returns a tuple of two collections, the first of which contains all items in the sequence for which \code{f} returned \code{true}, and the second of which contains all items for which \code{f} returned false.
\code{
defn split!<?T> (f: T -> True|False, xs:Seqable<?T>) ->
                 [Collection<T>&Lengthable, Collection<T>&Lengthable]
}

\subsubheader{split}
This function iterates through the items in the \code{xs} sequence, separating them into two sequences depending on whether calling \code{f} on the item returns \code{true} or \code{false}. The function returns a tuple of two sequences, the first of which contains all items in the sequence for which \code{f} returned \code{true}, and the second of which contains all items for which \code{f} returned false. The original argument sequence should not be used after this call. 
\code{
defn split<?T> (f: T -> True|False, xs:Seqable<?T>) -> [Seq<T>, Seq<T>]
}

\subsubheader{fork}
This function takes an argument sequence and returns two other sequences containing the same items. The argument sequence is only iterated through once. The original argument sequence should not be used after this call.
\code{
defn fork<?T> (xs:Seqable<?T>) -> [Seq<T>, Seq<T>]
}

If an additional argument, \code{n}, is given, then the function returns \code{n} new sequences containing the same items. 
\code{
defn fork<?T> (xs:Seqable<?T>, n:Int) -> [Seq<T>, Seq<T>]
}

\subsubheader{take-while}
This function constructs a sequence by taking items successively from the \code{xs} sequence as long as calling \code{f} upon the item returns \code{true}. The resulting sequence ends as soon as \code{xs} is empty or \code{f} returns \code{false}. The item for which \code{f} returns \code{false} is not included in the resulting sequence.
\code{
defn take-while<?T> (f: T -> True|False, xs:Seqable<?T>) -> Seq<T>
}

\subsubheader{take-until}
This function constructs a sequence by taking items successively from the \code{xs} sequence as long as calling \code{f} upon the item returns \code{false}. The resulting sequence ends as soon as \code{xs} is empty or \code{f} returns \code{true}. The item for which \code{f} returns \code{true} is included in the resulting sequence.
\code{
defn take-until<?T> (f: T -> True|False, xs:Seqable<?T>) -> Seq<T>
}

\subsubheader{seq-cat}
This function constructs a sequence by calling \code{f} upon each item on the \code{xs} sequence. \code{seq-cat} then returns the concatenation of all sequences returned by \code{f}.
\code{
defn seq-cat<?T,?R> (f:T -> Seqable<?R>, xs:Seqable<?T>) -> Seq<R>
}

These functions construct a sequence by iterating through the given sequences in parallel and calling \code{f} upon each pair (or triplet) of items from each sequence. \code{seq-cat} then returns the concatenation of all sequences returned by \code{f}.
\code{
defn seq-cat<?T,?S,?R> (f:(T,S) -> Seqable<?R>,
                        xs:Seqable<?T>,
                        ys:Seqable<?S>) -> Seq<R>
                        
defn seq-cat<?T,?S,?U,?R> (f:(T,S,U) -> Seqable<?R>,
                           xs:Seqable<?T>,
                           ys:Seqable<?S>,
                           zs:Seqable<?U>) -> Seq<R>
}

\subsubheader{all?}
This function iterates through the items in the sequence \code{xs} and calls \code{pred?} on each one. If \code{pred?} returns \code{true} for every item in the sequence \code{all?} returns true. If \code{pred?} returns \code{false} for any item, then \code{all?} immediately returns \code{false}. \code{all?} returns \code{true} when the collection is empty.
\code{
defn all?<?T> (pred?: T -> True|False, xs:Seqable<?T>) -> True|False
}

These functions iterate through the given sequences in parallel and calls \code{pred?} on each pair (or triplet) of items from each sequence. Iteration stops as soon as any sequence is empty. \code{all?} returns \code{true} if all calls to \code{pred?} returns \code{true}. \code{all?} returns \code{false} immediately after a call to \code{pred?} returns \code{false}.
\code{
defn all?<?T,?S> (pred?: (T,S) -> True|False,
                  xs:Seqable<?T>,
                  ys:Seqable<?S>) -> True|False
                  
defn all?<?T,?S,?U> (pred?: (T,S,U) -> True|False,
                     xs:Seqable<?T>,
                     ys:Seqable<?S>,
                     zs:Seqable<?U>) -> True|False
}

\subsubheader{none?}
This function iterates through the items in the sequence \code{xs} and calls \code{pred?} on each one. If \code{pred?} returns \code{true} for no item in the sequence then \code{none?} returns \code{true}. If \code{pred?} returns \code{true} for any item, then \code{none?} immediately returns \code{false}. \code{none?} returns \code{true} when the collection is empty.
\code{
defn none?<?T> (pred?: T -> True|False, xs:Seqable<?T>) -> True|False
}

These functions iterate through the given sequences in parallel and calls \code{pred?} on each pair (or triplet) of items from each sequence. Iteration stops as soon as any sequence is empty. \code{none?} returns \code{true} if no calls to \code{pred?} returns \code{true}. \code{none?} returns \code{false} immediately after a call to \code{pred?} returns \code{true}.
\code{
defn none?<?T,?S> (pred?: (T,S) -> True|False,
                   xs:Seqable<?T>,
                   ys:Seqable<?S>) -> True|False
                   
defn none?<?T,?S,?U> (pred?: (T,S,U) -> True|False,
                      xs:Seqable<?T>,
                      ys:Seqable<?S>,
                      zs:Seqable<?U>) -> True|False
}

\subsubheader{any?}
This function iterates through the items in the sequence \code{xs} and calls \code{pred?} on each one. If \code{pred?} returns \code{true} for no item in the sequence then \code{any?} returns \code{false}. If \code{pred?} returns \code{true} for any item, then \code{any?} immediately returns \code{true}. \code{any?} returns \code{false} when the collection is empty.
\code{
defn any?<?T> (pred?: T -> True|False, xs:Seqable<?T>) -> True|False
}

These functions iterate through the given sequences in parallel and calls \code{pred?} on each pair (or triplet) of items from each sequence. Iteration stops as soon as any sequence is empty. \code{any?} returns \code{false} if no calls to \code{pred?} returns \code{true}. \code{any?} returns \code{true} immediately after a call to \code{pred?} returns \code{true}.
\code{
defn any?<?T,?S> (pred?: (T,S) -> True|False,
                  xs:Seqable<?T>,
                  ys:Seqable<?S>) -> True|False
                  
defn any?<?T,?S,?U> (pred?: (T,S,U) -> True|False,
                     xs:Seqable<?T>,
                     ys:Seqable<?S>,
                     zs:Seqable<?U>) -> True|False
}

\subsubheader{count}
This function iterates through the items in the sequence \code{xs} and calls \code{pred?} on each one. \code{count} returns the number of times that \code{pred?} returned \code{true}.
\code{
defn count<?T> (pred?: T -> True|False, xs:Seqable<?T>) -> Int
}


\subsubheader{Sequence Constructors}
Sequence constructors are functions that take non-\code{Seq} objects and construct and return \code{Seq} objects from them.

\subsubheader{repeat}
This function creates an infinite sequence resulting from repeating \code{x} indefinitely.
\code{
defn repeat<?T> (x:?T) -> Seq<T>
}

This function creates a sequence resulting from repeating \code{x} for \code{n} number of times.
\code{
defn repeat<?T> (x:?T, n:Int) -> Seq<T> & Lengthable
}

\subsubheader{repeatedly}
This function creates an infinite sequence from the results of calling \code{f} repeatedly.
\code{
defn repeatedly<?T> (f:() -> ?T) -> Seq<T>
}

This function creates a sequence resulting from the results of calling \code{f} for \code{n} number of times.
\code{
defn repeatedly<?T> (f:() -> ?T, n:Int) -> Seq<T> & Lengthable
}

\subsubheader{repeat-while}
This function creates a sequence resulting from calling \code{f} repeatedly and including, in the result sequence, the unwrapped values of every \code{One} object returned by \code{f}. The sequence ends as soon as \code{f} returns a \code{None} object.
\code{
defn repeat-while<?T> (f: () -> Maybe<?T>) -> Seq<T>
}

\subsubheader{Sequence Operators}
Sequence operators are functions that take \code{Seq} objects as arguments and return a new \code{Seq} object.

\subsubheader{filter}
This function constructs a sequence by iterating through the \code{xs} and \code{sel} sequences in parallel and including items from the \code{xs} sequence in the result sequence only if the corresponding item from the \code{sel} sequence is \code{true}.
\code{
defn filter<?T> (xs:Seqable<?T>, sel:Seqable<True|False>) -> Seq<T>
}

\subsubheader{take-n}
This function constructs a sequence by taking the first \code{n} items from the \code{xs} sequence. The \code{xs} sequence must contain at least \code{n} items.
\code{
defn take-n<?T> (n:Int, xs:Seqable<?T>) -> Seq<T>
}

\subsubheader{take-up-to-n}
This function constructs a sequence by taking up to the first \code{n} items from the \code{xs} sequence.
\code{
defn take-up-to-n<?T> (n:Int, xs:Seqable<?T>) -> Seq<T>
}

\subsubheader{cat}
This function constructs a new sequence by concatenating the \code{a} sequence with the \code{b} sequence.
\code{
defn cat<?T> (a:Seqable<?T>, b:Seqable<?T>) -> Seq<T>
}

\subsubheader{cat-all}
This function constructs a new sequence by concatenating together all sequences in the \code{xss} sequence.
\code{
defn cat-all<?T> (xss: Seqable<Seqable<?T>>) -> Seq<T>
}

\subsubheader{join}
This function constructs a new sequence by including the item \code{y} in between each item in \code{xs}.
\code{
defn join<?T,?S> (xs:Seqable<?T>, y:?S) -> Seq<T|S>
}

\subsubheader{zip}   
This function constructs a new sequence by iterating through the sequences \code{xs} and \code{ys} in parallel and including the 2 element tuples formed from the items of each sequence. Iteration stops as soon as either sequence is empty.
\code{
defn zip<?T,?S> (xs:Seqable<?T>, ys:Seqable<?S>) -> Seq<[T,S]>
}

This function constructs a new sequence by iterating through the sequences \code{xs}, \code{ys}, and \code{zs}, in parallel and including the 3 element tuples formed from the items of each sequence. Iteration stops as soon as any sequence is empty.
\code{
defn zip<?T,?S,?U> (xs:Seqable<?T>,
                    ys:Seqable<?S>,
                    zs:Seqable<?U>) -> Seq<[T,S,U]>
}

\subsubheader{zip-all}
This function constructs a new sequence by iterating through all sequences in \code{xss} in parallel, and including the tuples formed from the items of each sequence. Iteration stops as soon as any sequence is empty.
\code{
defn zip-all<?T> (xss:Seqable<Seqable<?T>>) -> Seq<Tuple<T>>
}

\subsubheader{Sequence Reducers}
Sequence reducers are functions that take \code{Seq} objects as arguments and compute and return a non-\code{Seq} object.

\subsubheader{contains?}
This function returns \code{true} if the \code{xs} sequence contains the item \code{y}. Otherwise it returns \code{false}.
\code{
defn contains? (xs:Seqable<Equalable>, y:Equalable) -> True|False
}

\subsubheader{index-of}
This function returns the first index at which the item in the \code{xs} sequence is equal to the item \code{y}. If no item in \code{xs} is equal to \code{y} then \code{index-of} returns \code{false}.
\code{
defn index-of (xs:Seqable<Equalable>, y:Equalable) -> Int|False
}

\subsubheader{index-of!}
This function behaves identically to \code{index-of} except that it assumes there exists an item in \code{xs} that is equal to \code{y}.
\code{
defn index-of! (xs:Seqable<Equalable>, y:Equalable) -> Int
}

\subsubheader{split}
This function iterates through the items in the \code{xs} and \code{ss} sequence in parallel, and separates the items from \code{xs} into two collections depending on whether the corresponding item from \code{ss} is \code{true} or \code{false}. The function returns a tuple of two collections, the first of which contains all items in \code{xs} for which the corresponding item in \code{ss} is \code{true}, and the second of which contains the other items in \code{xs}.
\code{
defn split<?T> (xs:Seqable<?T>, ss:Seqable<True|False>) ->
                [Collection<T>&Lengthable, Collection<T>&Lengthable]
}

\subsubheader{count}
This function returns the number of items in the given sequence. If the sequence is a subtype of \code{Lengthable}, then the length is returned directly. Otherwise, the length is calculated by iterating through the entire sequence.
\code{
defn count (xs:Seqable) -> Int
}

\subsubheader{reduce}
This function computes the left oriented reduction using function \code{f} on the sequence \code{xs} starting with initial value \code{x0}. If \code{xs} is empty then \code{x0} is returned. Otherwise, the function \code{f} is applied on \code{x0} and the first element in \code{xs}, then \code{f} is applied again on that result and the second element in \code{xs}, then \code{f} is applied on that result and the third element in \code{xs}, and so forth until \code{xs} is empty. \code{reduce} returns the final result returned by \code{f}.
\code{
defn reduce<?T,?S> (f: (T, S) -> ?T, x0: ?T, xs:Seqable<?S>) -> T
}

If no initial value is provided, then the first element in \code{xs} is used as the initial value of the reduction. \code{xs} must not be empty.
\code{
defn reduce<?T,?S> (f: (T|S, T) -> ?S, xs:Seqable<?T>) -> T|S
}

\subsubheader{reduce-right}
This function computes the right oriented reduction using function \code{f} on the sequence \code{xs} with final value \code{xn}. If \code{xs} is empty then \code{xn} is returned. If \code{xs} has a single element, then \code{f} is applied on that element and \code{xn}. If \code{xs} has two elements, then f is applied on the first element and the result of applying \code{f} on the second element and \code{xn}. If \code{xs} has three elements, then \code{f} is applied on the first element and the result of applying \code{f} on the second element and the result of applying \code{f} on the third element and \code{xn}. Et cetera. 
\code{
defn reduce-right<?T,?S> (f: (S, T) -> ?T, xs:Seqable<?S>, xn:?T) -> T
}

If no final value is provided, then the last element in \code{xs} is used as the final value of the reduction. \code{xs} must not be empty.
\code{
defn reduce-right<?T,?S> (f: (T, T|S) -> ?S, xs:Seqable<?T>) -> T|S
}

\subsubheader{unique}
This function returns a list containing all unique items in the sequence \code{xs}.
\code{
defn unique<?T> (xs:Seqable<?T&Equalable>) -> List<T>
}

\subsubheader{lookup?}
This function iterates through the \code{xs} sequence, and looks for the first \code{KeyValue} pair whose key is equal to \code{k}. If such a pair is found, then its value is returned. If not then \code{default} is returned.
\code{
defn lookup?<?K,?V,?D> (xs:Seqable<KeyValue<?K&Equalable,?V>>,
                        k:K&Equalable,
                        default:?D) -> D|V
}

If no default value is provided, then \code{false} is returned if no appropriate \code{KeyValue} pair is found.
\code{
defn lookup?<?K,?V> (xs:Seqable<KeyValue<?K&Equalable,?V>>,
                     k:K&Equalable) -> False|V
}

\subsubheader{lookup}
This function behaves identically to \code{lookup?} except that it assumes that an appropriate \code{KeyValue} pair exists and returns its value.
\code{
defn lookup<?K,?V> (xs: Seqable<KeyValue<?K&Equalable,?V>>, k:K&Equalable) -> V
}

\subsubheader{fork-on-seq}
This function takes a sequence \code{xs}, and returns a tuple containing the results of calling the functions \code{f} and \code{g} with \code{xs}. The functions \code{f} and \code{g} are computed concurrently such that \code{xs} is iterated through only once. This function allows multiple values to be computed from a sequence that can only be iterated through once.
\code{
defn fork-on-seq<?T,?X,?Y> (xs:Seqable<?T>,
                            f:Seq<T> -> ?X,
                            g:Seq<T> -> ?Y) -> [X,Y]
}

This function takes a sequence \code{xs}, and returns a tuple containing the results of calling the functions \code{f}, \code{g}, and \code{h} with \code{xs}. The functions are computed concurrently such that \code{xs} is iterated through only once.
\code{
defn fork-on-seq<?T,?X,?Y,?Z> (xs:Seqable<?T>,
                               f:Seq<T> -> ?X,
                               g:Seq<T> -> ?Y,
                               h:Seq<T> -> ?Z) -> [X,Y,Z]
}

This function takes a sequence \code{xs}, and returns a tuple containing the results of calling every function in \code{fs} with \code{xs}. The functions are computed concurrently such that \code{xs} is iterated through only once.
\code{
defn fork-on-seq<?T,?S> (xs:Seqable<?T>,
                         fs:Seqable<(Seq<T> -> ?S)>) -> Tuple<S>
}

\subheader{Sorting}
Stanza's core library provides convenience functions for sorting collections.

\subsubheader{qsort!}
This function sorts the collection using the given comparison function and the quick sort algorithm.
\code{
defn qsort!<?T> (xs:IndexedCollection<?T>, is-less?:(T,T) -> True|False) -> False
}

This function sorts the collection using the \code{less?} multi and using the quick sort algorithm.
\code{
defn qsort!<?T> (xs:IndexedCollection<?T&Comparable>) -> False
}

This function sorts the collection by comparing the keys extracted using the \code{key} function and using the quick sort algorithm.
\code{
defn qsort!<?T> (key:T -> Comparable, xs:IndexedCollection<?T>) -> False
}

\subsubheader{lazy-qsort}
This function returns a lazily sorted sequence of the given sequence \code{xs} using the given comparison function and the quick sort algorithm.
\code{
defn lazy-qsort<?T> (xs:Seqable<?T>,
                     is-less?:(T,T) -> True|False) -> Collection<T> & Lengthable
}

This function returns a lazily sorted collection of the given sequence \code{xs} using the \code{less?} multi and the quick sort algorithm.
\code{
defn lazy-qsort<?T> (xs:Seqable<?T&Comparable>) -> Collection<T> & Lengthable
}

This function returns a lazily sorted collection of the given sequence \code{xs} by comparing keys extracted using the \code{key} function and using the quick sort algorithm.
\code{
defn lazy-qsort<?T> (key:T -> Comparable,
                     xs:Seqable<?T>) -> Collection<T> & Lengthable
}

\subheader{LivenessTracker}
Stanza's support for automatic garbage collection means that users do not need to manually delete objects after their use. However, it is often useful to know when an object has been reclaimed to perform additional cleanup operations. \code{LivenessTracker} and \code{LivenessMarker} objects are used to track whether an object is still live. 
\code{
deftype LivenessTracker<T>
deftype LivenessMarker
}

\subsubheader{LivenessTracker}
This function creates a \code{LivenessTracker} with an associated value to indicate the identity of the tracker.
\code{
defn LivenessTracker<?T> (value:?T) -> LivenessTracker<T>
}

\subsubheader{value}
This function can be used to retrieve the value associated with a tracker.
\code{
defn value<?T> (tracker:LivenessTracker<?T>) -> T
}

\subsubheader{marker}
This function is used to create or check upon a \code{LivenessMarker}.
\code{
defn marker (tracker:LivenessTracker) -> False|LivenessMarker
}

The first time this function is called on a \code{LivenessTracker} it is guaranteed to return its associated \code{LivenessMarker}. This marker may then be stored with an object whose liveness the user wishes to track. On subsequent calls to \code{marker}, the function will either return the created marker, which indicates that the marker is still live. Or it will return \code{false}, which indicates that the marker is now longer live, and hence neither is the object in which it is stored.
                
For example, consider tracking the liveness of values of type \code{Car}. Intuitively, it is helpful to imagine a \code{LivenessTracker} as a futuristic pistol capable of shooting a tracking beacon (its \code{LivenessMarker}) that sticks to the \code{Car}. Periodically, you may ask the tracker whether it is still receiving signal from the beacon, in which case the marker and hence the \code{Car} is still live. If the beacon is no longer sending a signal (\code{marker} returns \code{false}), then the marker and hence the \code{Car} is no longer live.

\subsubheader{marker!}
This function assumes that \code{marker} will return a \code{LivenessMarker} and returns it.
\code{
defn marker! (tracker:LivenessTracker) -> LivenessMarker
}

\subsubheader{add-gc-notifier}
This function registers a new garbage collection notifier with Stanza. Garbage collection notifiers are run after every garbage collection phase.
\code{
defn add-gc-notifier (f: () -> ?) -> False
}

\subheader{Files & Directories}

\subsubheader{file-exists?}
This function checks whether the given file exists given its name.
\code{
defn file-exists? (filename:String) -> True|False
}

\subsubheader{file-type}
These functions return the type of file in the passed \code{filename}. If \code{follow-symlinks?} is true, then the symlink is followed to its target and the type of the target filepath is returned. This function throws \code{FileTypeException} on error. By default, this function will follow symlinks. 

\code{
defn file-type (filename:String, follow-symlinks?:True|False) -> FileType
defn file-type (filename:String) -> FileType
}

The \code{FileType} has several subtypes:

\ulT{
  \liT{\code{RegularFileType}}
  \liT{\code{DirectoryType}}
  \liT{\code{SymbolicLinkType}}
  \liT{\code{OtherType}}
}

\subsubheader{rename-file}
This function renames a file from \code{path} to \code{new-path}. This function will throw \code{FileRenameError} on failure. See \aT[href=https://man7.org/linux/man-pages/man2/rename.2.html]{rename} for more information.
\code{
defn rename-file (path:String, new-path:String) -> False
}

\subsubheader{copy-file}
This function copies the contents of \code{old-path} to \code{new-path}, creating the \code{new-path} file if it doesn't exist. See \code{RandomAccessFile} for more info.
\code{
defn copy-file (old-path:String, new-path:String) -> False
}

\subsubheader{symlink}
This function creates a symlink at \code{linkpath} that points to \code{target} path. This function will throw \code{SymLinkError} on failure.
\code{
defn symlink (target:String, linkpath:String) -> False
}

\subsubheader{delete-file}
This function deletes the given file.
\code{
defn delete-file (path:String) -> False
}

\subsubheader{delete-recursive}
This function deletes the given path and if it is a directory, recursively deletes all files and directories inside that directory. If the \code{follow-symlinks?} argument is \code{true}, then any symlinks encountered during the descent will be followed and if the symlink points to a directory, the source of the symlink will also be deleted. Symlinks that point to other files will only delete the symlink and not the source file.
\code{
defn delete-recursive (path:String, follow-symlinks?:True|False) -> False
}

\subsubheader{resolve-path}
This function expands all symbolic links in the given path and returns an absolute path that points to the same file. If such a file does not exist then \code{false} is returned. The \code{resolve-path!} variant will thows a \code{PathResolutionError} exception on error.
\code{
defn resolve-path (path:String) -> String|False
defn resolve-path! (path:String) -> String
}

\subsubheader{split-filepath}
This function splits the passed \code{path} into two parts by looking for the last '/' character in \code{path}. If \code{path} is empty, then this function throws a \code{EmptyPath} exception. If \code{path} ends with a '/', then this function throws a \code{PathEndsWithSlash} exception. Both of these exceptions are subtypes of \code{PathException}.

NOTE: This function will not work with window's drive paths like \code{C:\\Users\\foo} that use the '\\' character as path separator.

\code{
defn split-filepath (path:String) -> [String, String]
}

\subsubheader{dir-files}
These functions return a tuple of strings containing the names of the files in the given directory \code{dirname}. The \code{include-parents?} flag is used to filter out the "." and ".." directories if present. These functions may throw \code{DirException} on error. For more information see \aT[href=https://man7.org/linux/man-pages/man3/readdir.3p.html]{readdir}.

\code{
defn dir-files (dirname:String, include-parents?:True|False) -> [String]
defn dir-files (dirname:String) -> [String]
}

\subsubheader{create-dir}
This function will create the passed \code{dirname} directory. If the \code{permissions} parameter is not provided, the default permissions will be \code{0o777}. This function will throw a \code{CreateDirException} on error. See \aT[href=https://man7.org/linux/man-pages/man2/mkdir.2.html]{mkdir} for more info.

\code{
defn create-dir (dirname:String, permissions:Int) -> False
defn create-dir (dirname:String) -> False
}

\subsubheader{create-dir-recursive}
This function will create directories needed for the steps in the passed \code{filepath}. This function behaves like \code{mkdir -p}. If \code{permissions} is not passed, then the default permissions of \code{0o777} will be used. On error, this function will throw \code{CreateDirException} or \code{CreateRecursiveDirException}.

\code{
defn create-dir-recursive (filepath:String, permissions:Int) -> False
defn create-dir-recursive (filepath:String) -> False
}


\subheader{System Utilities}
Stanza's core library provides convenience functions for manipulating the system environment.

\subsubheader{command-line-arguments}
The command line arguments used to invoke the program can be retrieved as an array of strings using the following function.
\code{
defn command-line-arguments () -> Array<String>
}

\subsubheader{current-time-ms}
This function returns the number of milliseconds elapsed since January 1, 1970.
\code{
defn current-time-ms () -> Long
}

\subsubheader{current-time-us}
This function returns the number of microseconds elapsed since January 1, 1970.
\code{
defn current-time-us () -> Long
}

\subsubheader{get-env}
This function returns the value associated with the environment variable with the given name. \code{false} is returned if there is no such variable.
\code{
defn get-env (name:String) -> String|False
}

\subsubheader{set-env}
These functions associate the environment variable of the given name with the given value. If the environment variable already exists then its value is overwritten if the \code{overwrite} flag is \code{true}, otherwise its old value is kept. If no \code{overwrite} flag is given then its default value is \code{true}.
\code{
defn set-env (name:String, value:String, overwrite:True|False) -> False
defn set-env (name:String, value:String) -> False
}

\subsubheader{unset-env}

This function deletes a variable \code{name} from the environment. If \code{name} does not exist, this function leaves the environment unchanged and returns successfully. This function will throw a \code{UnsetEnvException} on error. See \aT[href=https://man7.org/linux/man-pages/man3/setenv.3.html]{unsetenv} from the standard C library.

\code{
defn unset-env (name:String) -> False
}

\subsubheader{call-system}
This function executes the given program with the given arguments. The sequence of strings for \code{args} is passed to the traditional \code{char** argvs} argument for the C \code{main} function. Note that because of this, the first item in \code{args} is expected to be the name of the program itself. 

If the call fails with an error then the function throws a \code{SystemCallException}, otherwise it returns the integer status code of the call.
\code{
defn call-system (file:String, args:Seqable<String>) -> Int
}

\subsubheader{call-system-and-get-output}
This function executes the given program with the given arguments. The sequence of strings for \code{args} is passed to the traditional \code{char** argvs} argument for the C \code{main} function. Note that because of this, the first item in \code{args} is expected to be the name of the program itself.

If the call fails with an error then the function throws a \code{SystemCallException}, otherwise it returns the printed output from the program output and error streams. 

\code{
defn call-system-and-get-output (file:String, args:Seqable<String>) -> String
}

\subsubheader{replace-current-process}
This function replaces the current process with the execution of the given program with the given arguments. The sequence of strings for \code{args} is passed to the traditional \code{char** argvs} argument for the C \code{main} function. Note that because of this, the first item in \code{args} is expected to be the name of the program itself.

If the call fails with an error then the function throws a \code{SystemCallException}, otherwise the current process is replaced.

If the \code{resolve-path?} argument is set to \code{true}, then the \code{path} argument is looked up using standard system shell conventions: e.g. it first looks in the directories listed on the \code{PATH}, etc. Otherwise, the \code{path} should point to a concrete file. The \code{resolve-path?} argument is \code{true} by default.

\code{
defn replace-current-process (path:String,
                              args:Tuple<String>,
                              resolve-path?:True|False) -> Void
defn replace-current-process (path:String,
                              args:Tuple<String>) -> Void
}

\subsubheader{exit}

This function will call the underlying libc \aT[href=https://man7.org/linux/man-pages/man3/exit.3.html]{exit} function.

\code{
defn exit (code:Int) -> Void
}

\subheader{Timer}
\code{Timers} are used for measuring elapsed time at various granularities.
\code{
deftype Timer
}

\subsubheader{MillisecondTimer}
This function creates a timer with millisecond granularity and an associated name.
\code{
defn MillisecondTimer (name:String) -> Timer
}

\subsubheader{MicrosecondTimer}
This function creates a timer with microsecond granularity and an associated name.
\code{
defn MicrosecondTimer (name:String) -> Timer
}

\subsubheader{PiggybackTimer}
This function creates a timer that piggybacks off of the time measured by another timer. Time only elapses for a piggyback timer if the timer it is piggybacking is running. A \code{PiggybackTimer} returns time in the same granularity as the timer it is piggybacking.
\code{
defn PiggybackTimer (name:String, t:Timer) -> Timer
}

\subsubheader{start}
This function starts the timer. It is an error to start a \code{Timer} that is already running.
\code{
defn start (t:Timer) -> False
}

\subsubheader{stop}
This function stops the timer. It is an error to stop a \code{Timer} that is not running.
\code{
defn stop (t:Timer) -> False
}

\subsubheader{time}
This function retrieves the current time elapsed by the timer. The unit of the number returned is dependent upon how the timer is created.
\code{
defn time (t:Timer) -> Long
}

\header{Math Package}
The \code{math} package contains basic mathematical quantities and operations.

\subheader{Quantities}

\subsubheader{PI}
This value contains a double-precision definition of the mathematical quantity pi.
\code{
val PI : Double
}

\subsubheader{PI-F}
This value contains a single-precision definition of the mathematical quantity pi.
\code{
val PI-F : Float
}

\subheader{Basic Operations}

\subsubheader{exp}
This function computes e to the power of \code{x}.
\code{
defn exp (x:Double) -> Double
defn exp (x:Float) -> Float
}

\subsubheader{log}
This function computes the natural logarithm of \code{x}.
\code{
defn log (x:Double) -> Double
defn log (x:Float) -> Float
}

\subsubheader{log10}
This function computes the base 10 logarithm of \code{x}.
\code{
defn log10 (x:Double) -> Double
defn log10 (x:Float) -> Float
}

\subsubheader{pow}
This function computes \code{x} to the power of \code{y}.
\code{
defn pow (x:Double, y:Double) -> Double
defn pow (x:Float, y:Float) -> Float
}

\subsubheader{sin}
This function computes the sine of \code{x}.
\code{
defn sin (x:Double) -> Double
defn sin (x:Float) -> Float
}

\subsubheader{cos}
This function computes the cosine of \code{x}.
\code{
defn cos (x:Double) -> Double
defn cos (x:Float) -> Float
}

\subsubheader{tan}
This function computes the tangent of \code{x}.
\code{
defn tan (x:Double) -> Double
defn tan (x:Float) -> Float
}

\subsubheader{asin}
This function computes the inverse sine of \code{x}.
\code{
defn asin (x:Double) -> Double
defn asin (x:Float) -> Float
}

\subsubheader{acos}
This function computes the inverse cosine of \code{x}.
\code{
defn acos (x:Double) -> Double
defn acos (x:Float) -> Float
}

\subsubheader{atan}
This function computes the inverse tangent of \code{x}.
\code{
defn atan (x:Double) -> Double
defn atan (x:Float) -> Float
}

\subsubheader{atan2}
This function computes the inverse tangent of \code{y} divided by \code{x}. The signs of both arguments are used to determine the quadrant of the result.
\code{
defn atan2 (y:Double, x:Double) -> Double
defn atan2 (y:Float, x:Float) -> Float
}

\subsubheader{sinh}
This function computes the hyperbolic sine of \code{x}.
\code{
defn sinh (x:Double) -> Double
defn sinh (x:Float) -> Float
}

\subsubheader{cosh}
This function computes the hyperbolic cosine of \code{x}.
\code{
defn cosh (x:Double) -> Double
defn cosh (x:Float) -> Float
}

\subsubheader{tanh}
This function computes the hyperbolic tangent of \code{x}.
\code{
defn tanh (x:Double) -> Double
defn tanh (x:Float) -> Float
}

\subsubheader{ceil}
This function computes the ceiling of \code{x}.
\code{
defn ceil (x:Double) -> Double
defn ceil (x:Float) -> Float
}

\subsubheader{floor}
This function computes the floor of \code{x}.
\code{
defn floor (x:Double) -> Double
defn floor (x:Float) -> Float
}

\subsubheader{round}
This function rounds \code{x} to the nearest integer.
\code{
defn round (x:Double) -> Double
defn round (x:Float) -> Float
}

\subsubheader{to-radians}
This function converts an angle expressed in degrees to radians.
\code{
defn to-radians (x:Double) -> Double
defn to-radians (x:Float) -> Float
}

\subsubheader{to-degrees}
This function converts an angle expressed in radians to degrees.
\code{
defn to-degrees (x:Double) -> Double
defn to-degrees (x:Float) -> Float
}

\header{Collections Package}
The \code{collections} package consists of functions and types useful for managing collection datastructures. 

\subheader{Vector}
A \code{Vector} object represents a dynamically growing array of items. Like arrays, each item can be accessed using an integer index. However, a \code{Vector} object can be resized after creation.
\code{
deftype Vector<T> <: IndexedCollection<T>
}

\code{Vector} is a subtype of \code{IndexedCollection} and thus implements the appropriate methods for retrieving elements, assigning elements, and retrieving its length. When assigning an item, \code{v}, to an index \code{i}, if \code{i} is less than the length of the vector then the item at that location is overwritten by \code{v}. If \code{i} is equal to the length of the vector, then \code{v} is added to the end of the vector.

\subsubheader{Vector}
This function creates a \code{Vector} for holding objects of type \code{T} initialized with a capacity of \code{cap}.
\code{
defn Vector<T> (cap:Int) -> Vector<T>
}

If no capacity is given, then the default capacity is \code{8}.
\code{
defn Vector<T> () -> Vector<T>
}

\subsubheader{to-vector}
This function creates a new \code{Vector} from the elements in the sequence \code{xs}.
\code{
defn to-vector<T> (xs:Seqable<T>) -> Vector<T>
}

\subsubheader{add}
This function adds the given value to the end of the vector.
\code{
defn add<?T> (v:Vector<?T>, value:T) -> False
}

\subsubheader{add-all}
This function adds all of the values in the sequence \code{vs} to the end of the vector.
\code{
defn add-all<?T> (v:Vector<?T>, vs:Seqable<T>) -> False
}

\subsubheader{clear}
This function removes every item in the vector.
\code{
defn clear (v:Vector) -> False
}

\subsubheader{pop}
This function removes the last item in the vector and returns it. The vector must not be empty.
\code{
defn pop<?T> (v:Vector<?T>) -> T
}

\subsubheader{peek}
This function returns the last item in the vector. The vector must not be empty.
\code{
defn peek<?T> (v:Vector<?T>) -> T
}

\subsubheader{remove}
This function removes the item at index \code{i} in the vector and returns the removed item. \code{i} must be less than the length of the vector. The operation takes time proportional to the difference between the length of the vector and \code{i}.
\code{
defn remove<?T> (v:Vector<?T>, i:Int) -> T
}

\subsubheader{remove}
This function removes the items within the range \code{r} from the vector. The range must be a \italic{dense index range} with respect to the vector. The operation takes time proportional to the difference between the length of the vector and the end of the range.
\code{
defn remove (v:Vector, r:Range) -> False
}

\subsubheader{update}
This function either overwrites or removes items from the vector by calling the function \code{f} on each item in the vector. For each item in the vector, if \code{f} returns a \code{One} object, then the item is overwritten with the wrapped value in the \code{One} object. If \code{f} returns a \code{None} object, then the item is removed from the vector. \code{update} is compatible with the \code{for} macro form.
\code{
defn update<?T> (f: T -> Maybe<T>, v:Vector<?T>) -> False
}

\subsubheader{remove-item}
This function removes the first occurrence of the item \code{x} in the vector \code{v} if it exists. The function returns \code{true} if an item was removed, or \code{false} otherwise.
\code{
defn remove-item<?T> (v:Vector<?T&Equalable>, x:T&Equalable) -> True|False
}

\subsubheader{remove-when}
This function calls the function \code{f} on every item in the vector \code{v}. Items for which \code{f} returned \code{true} are removed.
\code{
defn remove-when<?T> (f: T -> True|False, v:Vector<?T>) -> False 
}

\subsubheader{trim}
This function sets the capacity of the vector to be equal to the size of the vector, thus removing storage for additional elements.
\code{
defn trim (v:Vector) -> False   
}

\subsubheader{shorten}
This function truncates the given vector down to the specified size. The given size must be less than or equal to the length of the vector.
\code{
defn shorten (v:Vector, size:Int) -> False
}

\subsubheader{lengthen}
This function pads the given vector up to the specified size by adding \code{x} repeatedly to the end of the vector. The given size must be greater than or equal to the length of the vector.
\code{
defn lengthen<?T> (v:Vector<?T>, size:Int, x:T) -> False
}

\subsubheader{set-length}
This function sets the length of the vector to the new specified length. If the given length is shorter than the current length then the vector is truncated. Otherwise the vector is padded using the item \code{x}.
\code{
defn set-length<?T> (v:Vector<?T>, length:Int, x:T) -> False   
}

\subsubheader{map}
This function creates a new \code{Vector} from the results of applying the function \code{f} repeatedly to each item in the given vector \code{v}. Note that the element type, \code{R}, of the result vector must be given explicitly.
\code{
defn map<R,?T> (f: T -> R, v:Vector<?T>) -> Vector<R>
}

\subheader{Queue}
A \code{Queue} represents an indexed collection with support for acting as a first in last out (FIFO) datastructure.
\code{
deftype Queue<T> <: IndexedCollection<T>
}

\code{Queue} is a subtype of \code{IndexedCollection} and thus implements the appropriate methods for retrieving elements, assigning elements, and retrieving its length. When assigning an item, \code{v}, to an index \code{i}, if \code{i} is not negative and less than the length of the queue then the item at that location is overwritten by \code{v}. If i is equal to \code{-1}, then \code{v} is added to the front of the queue.

\subsubheader{Queue}
This function creates a \code{Queue} for holding objects of type \code{T} initialized with a capacity of \code{cap}.
\code{
defn Queue<T> (cap:Int) -> Queue<T>
}

If no capacity is given, then the default capacity is \code{8}.
\code{
defn Queue<T> () -> Queue<T>
}

\subsubheader{add}
This function adds the given item to the front of the queue. The corresponding indices of all existing entries in the queue increase by \code{1}.
\code{
defn add<?T> (q:Queue<?T>, x:T) -> False
}

\subsubheader{pop}
This function removes and returns the item at the back of the queue. The queue must not be empty.
\code{
defn pop<?T> (q:Queue<?T>) -> T
}

\subsubheader{peek}
This function returns the item at the back of the queue. The queue must not be empty.
\code{
defn peek<?T> (q:Queue<?T>) -> T
}

\subsubheader{clear}
This function removes all items in the queue.
\code{
defn clear (q:Queue) -> False
}

\subheader{Set}
A \code{Set} represents a collection unique items of type \code{K}.
\code{
deftype Set<K> <: Collection<K> & Lengthable
}

\code{Set} is a subtype of \code{Collection} and must implement an appropriate method for the \code{to-seq} multi that allows for the set to be viewed as a sequence of \code{K} items.

\code{Set} is a subtype of \code{Lengthable} and must implement an appropriate method for the \code{length} multi that returns the number of unique items in the set.

Mandatory minimal implementation: \code{to-seq}, \code{length}, \code{add}, \code{remove}, \code{get}, \code{clear}.

\subsubheader{add}
\code{
defmulti add<?K> (s:Set<?K>, k:K) -> True|False
}
This multi adds the given item \code{k} to the set \code{s}. If the item was added succesfully, i.e. the item did not already exist in the set, then \code{add} returns \code{true}. Otherwise, the multi returns \code{false}.

\subsubheader{remove}
\code{
defmulti remove<?K> (s:Set<?K>, k:K) -> True|False
}
This multi removes the given item \code{k} from the set \code{s}. If the item was removed succesfully, i.e. the item existed in the set, then \code{remove} returns \code{true}. Otherwise, the multi returns \code{false}.

\subsubheader{get}
\code{
defmulti get<?K> (s:Set<?K>, k:K) -> True|False
}
This multi checks whether the given item \code{k} exists in the set \code{s}. 

\subsubheader{clear}
\code{
defmulti clear (s:Set) -> False
}
This multi removes all items from the given set.

\subheader{Table}
A \code{Table} represents a mapping between key objects of type \code{K} and value objects of type \code{V}.
\code{
deftype Table<K,V> <: Collection<KeyValue<K,V>> & Lengthable   
}

\code{Table} is a subtype of \code{Collection} and must implement an appropriate method for the \code{to-seq} multi that allows for the table to be viewed as a sequence of \code{KeyValue} pairs.

\code{Table} is a subtype of \code{Lengthable} and must implement an appropriate method for the \code{length} multi that returns the number of \code{KeyValue} pairs currently in the table.

Mandatory minimal implementation: \code{to-seq}, \code{length}, \code{set}, \code{get?}, \code{default?}, \code{remove}, \code{clear}.

\subsubheader{set}
This multi associates the key object \code{k} with the value object \code{v} in the table.
\code{
defmulti set<?K,?V> (t:Table<?K,?V>, k:K, v:V) -> False
}

\subsubheader{get?}
This multi retrieves the value object associated with the key object \code{k} in the table. If there is no such key in the table then the default value \code{d} is returned.
\code{
defmulti get?<?K,?V,?D> (t:Table<?K,?V> k:K, d:?D) -> V|D
}

\subsubheader{default?}
This multi returns the optional default value associated with the table. If a default value is provided then it is returned when accessing keys that do not exist in the table.
\code{
defmulti default?<?V> (t:Table<?,?V>) -> Maybe<V>
}

\subsubheader{remove}
This multi removes the given key and its associated value from the table. If there is a key then the function returns \code{true}. Otherwise the function returns \code{false}.
\code{
defmulti remove<?K> (t:Table<?K,?>, k:K) -> True|False
}

\subsubheader{clear}
This multi removes all keys and all values in the table.
\code{
defmulti clear (t:Table) -> False
}

\subsubheader{get?}
This function returns the value associated with the given key if it exists, otherwise it returns \code{false}.
\code{
defn get?<?K,?V> (t:Table<?K,?V>, k:K) -> V|False
}

\subsubheader{get}
This multi assumes that the given \code{k} exists in the table and returns its associated value, or that a default value is provided. A default method, defined in terms of \code{get?}, is provided but users may provide customized methods for subtypes of \code{Table} for efficiency purposes.
\code{
defmulti get<?K,?V> (t:Table<?K,?V>, k:K) -> V
}

\subsubheader{key?}
This multi returns \code{true} if the given key \code{k} exists in the table. A default method, defined in terms of \code{get?}, is provided but users may provide customized methods for subtypes of \code{Table} for efficiency purposes.
\code{
defmulti key?<?K> (t:Table<?K,?>, k:K) -> True|False   
}

\subsubheader{keys}
This multi returns a sequence containing all keys in the table. A default method, defined in terms of \code{to-seq}, is provided but users may provide customized methods for subtypes of \code{Table} for efficiency purposes.
\code{
defmulti keys<?K> (t:Table<?K,?>) -> Seqable<K>   
}

\subsubheader{values}
This multi returns a sequence containing all values in the table. A default method, defined in terms of \code{to-seq}, is provided but users may provide customized methods for subtypes of \code{Table} for efficiency purposes.
\code{
defmulti values<?V> (t:Table<?,?V>) -> Seqable<V>
}

\subsubheader{empty?}
This function returns \code{true} if the table contains no keys or values.
\code{
defn empty? (t:Table) -> True|False
}

\subheader{HashTable}

A \code{HashTable} provides an efficient implementation of tables for keys of type \code{Equalable}. Key type \code{K} must be a subtype of \code{Equalable}.
\code{
deftype HashTable<K,V> <: Table<K,V>
}

\code{HashTable} is a subtype of \code{Table} and support all necessary methods for correct table operations.

\subsubheader{HashTable}
This function creates a \code{HashTable} with key objects of type \code{K} and value objects of type \code{V}. A \code{hash} function that maps key objects to integers, an optional default value, and an initial capacity must be provided. If provided, the default value is returned for retrieval operations when the key does not exist.
\code{
defn HashTable<K,V> (hash: K -> Int,
                     default:Maybe<V>,
                     initial-capacity:Int) -> HashTable<K,V>
}

If unspecified, the default capacity is \code{32}.
\code{
defn HashTable<K,V> (hash: K -> Int, default:V) -> HashTable<K,V>
}

If unspecified, then there is no default value.
\code{
defn HashTable<K,V> (hash: K -> Int) -> HashTable<K,V>
}

This function creates a \code{HashTable} that uses the \code{hash} multi to compute hashes for its keys, and a default value of \code{V}. \code{K} must be a subtype of both \code{Hashable} and \code{Equalable}.
\code{
defn HashTable<K,V> (default:V) -> HashTable<K,V>
}

If unspecified, then there is no default value.
\code{
defn HashTable<K,V> () -> HashTable<K,V>
}

\header{Reader Package}
The \code{reader} package contains the implementation of Stanza's s-expression reader.

\subsubheader{read-file}
This function reads in the given file and returns its contents as a list of s-expressions.
\code{
defn read-file (filename:String) -> List
}

\subsubheader{read-all}
This function converts the given string into a list of s-expressions.
\code{
defn read-all (text:String) -> List
}

\subsubheader{read-all}
This function reads in all characters from the given string input stream and returns a list of s-expressions.
\code{
defn read-all (stream:StringInputStream) -> List
}

\subsubheader{read}
This function reads a single s-expression from the string input stream.
\code{
defn read (stream:StringInputStream) -> ?
} 

\header{Macro Utilities Package}

The \code{macro-utils} package contains functions for manipulating s-expressions useful for macro writers.

\subheader{Utility Functions}

\subsubheader{tagged-list?}
This function returns \code{true} if the given form is a list (or list wrapped in a \code{Token}) whose first element is equal to the given symbol, or is a token whose wrapped value is equal to the given symbol.
\code{
defn tagged-list? (form, tag:Symbol) -> True|False
}

\subheader{S-Expression Template Engine}
The template engine can be thought of as an advanced search and replace utility that operates on s-expressions instead of on strings. 

\subsubheader{fill-template}
This function returns the result of performing search and replace operations on a template form given a collection of desired replacements.
\code{
defn fill-template (template, replacements:Collection<KeyValue<Symbol,?>>) -> ?
}

The replacements are expressed as \code{KeyValue} pairs where the key holds the symbol that should be replaced, and the value holds what the key should be replaced by. Values can be objects of any type, but there are a set of distinguished template forms with special replacement rules.

\subsubheader{Simple Replacements}
Consider the following template :
\code{
val form = `(
   defn myfunction (arg : argtype) :
      body)
}
and the desired replacements :
\code{
val replacements = [
   `myfunction => `print-int
   `arg => `x
   `argtype => `Int
   `body => `(println("Inside print-int"),
              println(x))]
}

Then the following call to \code{fill-template} :
\code{
fill-template(form, replacements)
}
will return the following replaced form :
\code{
`(defn print-int (x : Int) :
     (println("Inside print-int"),
      println(x)))
}

Observe that occurrences of the keys in \code{replacements} in the template form are systematically replaced with their associated values.

\subsubheader{Splice Template}
The splice template allows for a symbol to be replaced by multiple values. The following function creates a splice template. Note that the given item must be either a \code{Collection} or a \code{Token} wrapping over a \code{Collection}.
\code{
defn splice (item:Collection|Token) -> SpliceTemplate
}

Consider again the previous example. Notice that, the key \code{`body} was associated with a 2 element list. And in the resulting form, the occurrence of \code{body} was replaced with the 2 element list.

By using the splice template we can replace occurrences of \code{`body} directly with the elements inside the list and avoid an extra pair of parenthesis.

Consider the following template :
\code{
val form = `(
   defn myfunction (arg : argtype) :
      body)
}
and the desired replacements :
\code{
val replacements = [
   `myfunction => `print-int
   `arg => `x
   `argtype => `Int
   `body => splice(`(println("Inside print-int"),
                     println(x)))]
}

Then the following call to \code{fill-template} :
\code{
fill-template(form, replacements)
}
will return the following replaced form :
\code{
`(defn print-int (x : Int) :
     println("Inside print-int")
     println(x))
}

\subsubheader{Nested Template}
The nested template allows for a pattern to be repeatedly replaced using a different set of replacements each time. The following function creates a nested template.
\code{
defn nested (items:Collection<Collection<KeyValue<Symbol,?>>>) -> NestedTemplate
}

Consider the following template :
\code{\#{
val form = `(
   defn myfunction (args{arg : argtype}) :
      body)
\#}}
and the desired replacements :
\code{
val replacements = [
   `myfunction => `print-int
   `args => nested $ [
      [`arg => `x, `argtype => `Int]
      [`arg => `y, `argtype => `String]
      [`arg => `z, `argtype => `Char]]
   `body => `(println(x))]
}

Then the following call to \code{fill-template} :
\code{
fill-template(form, replacements)
}
will return the following replaced form :
\code{
`(defn print-int (x:Int, y:String, z:Char) :
     (println(x)))
}

Note, specially, the syntax in the template when using a nested template replacement. If the replacement is a nested template, then the template form key \italic{must} have form :
\code{\#{
key{pattern ...}
\#}}

Note that the set of replacements used during each replacement of a nested template also includes all the entries from the parent replacements.

For example, if we use the following replacements on the same template form :
\code{
val replacements = [
   `myfunction => `print-int
   `argtype => `Int
   `args => nested $ [
      [`arg => `x]
      [`arg => `y]
      [`arg => `z, `argtype => `Char]]
   `body => `(println(x))]
}

Then the call to \code{fill-template} :
\code{
fill-template(form, replacements)
}
will return the following replaced form :
\code{
`(defn print-int (x:Int, y:Int, z:Char) :
     (println(x)))
}

Notice that the \code{`argtype} replacement value is inherited from the parent bindings.

\subsubheader{Plural Template}
The plural template is similar to the nested template and allows for a pattern to be repeatedly replaced using a different set of replacements each time. The difference is that the plural template allows the user to specify \italic{for each symbol} what it should be replaced with each time, whereas the nested template requires the user to specify \italic{for each replacement} what the set of replacements should be.

The following function creates a plural template given a sequence of replacements, each replacement being a sequence itself. Note that the length of each replacement value must be equal.
\code{
defn plural (entries: Seqable<KeyValue<Symbol, Seqable>>) -> PluralTemplate
}

Consider the following template :
\code{\#{
val form = `(
   defn myfunction (args{arg : argtype}) :
      body)
\#}}
and the desired replacements :
\code{
val replacements = [
   `myfunction => `print-int
   `args => plural $ [
      `arg => `(x y z)
      `argtype => `(Int String Char)]
   `body => `(println(x))]
}

Then the following call to \code{fill-template} :
\code{
fill-template(form, replacements)
}
will return the following replaced form :
\code{
`(defn print-int (x:Int, y:String, z:Char) :
     (println(x)))
}

\subsubheader{Choice Template}
The choice template allows for a symbol to be replaced with a pattern selected from amongst a list of patterns depending on the replacement value.

The following function creates a choice template given the choice \code{n}.
\code{
defn choice (n:Int) -> ChoiceTemplate
}

The following convenience function also allows for users to create a choice template given a boolean value, where \code{false} is equivalent to choice \code{0} and \code{true} is equivalent to choice \code{1}.
\code{
defn choice (b:True|False) -> ChoiceTemplate
}

Consider the following template :
\code{\#{
val form = `(
   defn myfunction (x:Int) :
      body{
         println(x)
      }{
         x + 1
      }{
         fatal(x)
      })
\#}}
and the desired replacements :
\code{
val replacements = [
   `myfunction => `print-int
   `body => choice(1)]
}

Then the following call to \code{fill-template} :
\code{
fill-template(form, replacements)
}
will return the following replaced form :
\code{
`(defn print-int (x:Int) :
     x + 1)
}

Note, specially, the syntax in the template when using a choice template replacement. If the replacement is a choice template, then the template form key \italic{must} have form :
\code{\#{
key{pattern0 ...}{pattern1 ...}{pattern2 ...}
\#}}

We can select which pattern is produced by changing the index we use to create the choice template. The following replacements :
\code{
val replacements = [
   `myfunction => `print-int
   `body => choice(2)]
}
will result in the following replaced form :
\code{
`(defn print-int (x:Int) :
     fatal(x))
}
